#! an

func main() {
	print("Content-type: text/html\n\n"); 
	webinit();
	
	print(
	'<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN">
	'<html>
	'<head>
	'<title>Guestbook!</title>
	'</head>
	'<body bgcolor="white">
	'Please <a href="#sign">sign</a> the guestbook<br>
	);
	
	hits = 0;
	file = fopen("/httpd/hits.txt", "r");
	if (file) {
		hits = getline(file);
		fclose(file);
	}
	hits++;
	file = fopen("/httpd/hits.txt", "w");
	if (file) {
		fprint(file, hits);
		fclose(file);
	}
	print(
	'This page has had ' @ hits @ ' hits since dawn of time.<br>
	);
	
	if (webexist("submitted")) {
		file = fopen("/httpd/entries.txt", "a");
		if (file) {
			a = '<hr>\n<p>From: <b>' @ webparam("name") @ '</b><br>\n';
			a @= 'IP address: ' @ webinfo("IP") @ '<br>\n';
			a @= 'Browser: ' @ webinfo("BROWSER") @ '<br>\n';
			a @= '<br>\n' @ webparam("message") @ '<br></p>\n';
			fprint(file, a);
			fclose(file);
		}
	}	
	printnl("This machine has been up for " @ showup() @ "<br>");
	file = fopen("/httpd/entries.txt", "r");
	if (file) {
		while ((a = getline(file)) != "") {
			print(a);
		} 
		fclose(file);
	}
	print(
	'<p><hr><a name="sign">Leave a message</a>
	'<form method="post" action="guestbook.an">
	'<table>
	'<tr><td>Name: </td>
	'<td><input name="name" type="text"></td>
	'<tr><td>Message: </td>
	'<td><textarea name="message" cols=40 rows=4></textarea></td>
	'<tr><td>&nbsp;</td><td><input name="submitted" type="submit"></td>
	'</table>
	'<br>
	'<a href="http://validator.w3.org/check/referer"><font
        'size="-2">Validated HTML 4.0 Transitional</font></a>
	'</form>
	'</body>
	'</html>
	);
}

func showup() {
	a = uptime();
	
	return isess(a / 86400," day") @ ", " @ isess(a%86400 / 3600, " hour") @ ", " @ isess(a%86400%3600/60, " minute") @ " and " @ isess(a%86400%3600%60, " second");	
}

func isess(a, b) {
	
	b = a @ b;
	if (a != 1)
		b @= "s";
	return b;
	
}
