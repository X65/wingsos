C
/* Memory Map so far... Tabs of size 12 BTW :)

0000-3fff	  - Free bank 0 kernel space
4000-7fff	  - Vic Memory
8000-bfff	  - Microkernel
c000-d000	  - VIC or free mem
d000-e000	  - IO or VIC
e000-ffff	  - VIC or free mem
010000-ffffff - Processes & Shared library's

*/

	.(
	
	.include <kern.i65>
	.include <proc.i65>
	.include <chips.i65>
	.include <exception.i65>
	
	.text $010002
	
	.al
	.xl
	
S_initK		jmp initK

&errno		.word 0
&S_BootDev	.word 0
&S_RLEnable	.word 0
&S_segptr	jsr @S_segadd
		tay
		inx
		inx
&S_segoff	lda #0
&S_segadd	clc
		adc @0,x
		rtl
&S_send		jmp send_S
&S_sendChan	jmp sendchan_S
&S_sendPulse	jmp sendPulse_S
&S_recv		jmp receive_S
&S_reply	jmp reply_S
&S_setErr	jmp setErr_S
&S_chkrecv	jmp chkrecv_S

&S_conChan	jmp conChan_S
&S_remCon	jmp remCon_S
&S_makeChan	jmp makeChan_S
&S_dup		jmp dup_S
&S_getSCOID	jmp getSCOID_S

&S_yield	jmp yield_S
&S_exit		jmp exit_S
&S_retexit	jmp retexit_S
&S_kill		jmp kill_S
&S_createT	jmp createT_S
&S_killTh	jmp killTh_S
&S_setPrior	jmp setPrior_S
&S_getPSInfo 	jmp getPSInfo_S
&S_ipcInfo	jmp ipcInfo_S
&S_getPID	jmp getPID_S
&S_getPPID	jmp getPPID_S
&S_setSpeed	jmp setSpeed_S
&S_chdir	jmp chdir_S
&S_getcwd	jmp getcwd_S

&S_moven	jmp moven_S
&S_debug	jmp debug_S

&S_getTINT	jmp getTINT_S
&S_setTINT	jmp setTINT_S
&S_getFlags	jmp getFlags_S
&S_setFlags	jmp setFlags_S
&S_getTimer	jmp getTimer_S
&S_setTimer	jmp setTimer_S
&S_waitOnQue	jmp waitOnQue_S
&S_wakeupQue	jmp wakeupQue_S
&S_getRast	jmp getRast_S
&S_setRast	jmp setRast_S
&S_getCal	jmp getCal_S
&S_setCal	jmp setCal_S
&S_kalloc	jmp kalloc_S
&S_kfree	jmp kfree_S
&S_getNMI	jmp getNMI_S
&S_getStart	jmp getStart_S
&S_setSCOID	jmp setSCOID_S
&S_getpdir	jmp getpdir_S
&S_isCon	jmp isCon_S
&S_getTID	jmp getTID_S
&S_getRPID	jmp getRPID_S
&S_try		jmp try_S
&S_throw	jmp throw_S
&S_popex	jmp popex_S
&S_minStack	jmp minStack_S
&S_wakeAllQ	jmp wakeAllQ_S
&S_waitCond	jmp waitCond_S

try_S	.(
	phb
	pha
	sta EXCEPT_DP
	stz EXCEPT_Type
	!KER_ENT_DBR
	ldx Current
	lda !TH_Except,x
	sta EXCEPT_Next
	tdc
	sta !TH_Except,x
	tsc
	sta EXCEPT_Stack
	lda 3,s
	sta EXCEPT_RetAdd
	lda 5,s
	sta EXCEPT_RetAdd+2
	pld
	plb
	ldx #0
	rtl
	.)

throw_S	.(
	!KER_ENT_DBR
	ldx Current
	lda !TH_Except,x
	bne havex
	jmp exit_S
havex	tcd
	lda 4,s
	sta EXCEPT_Type
	lda 6,s
	sta EXCEPT_Data
	lda 8,s
	sta EXCEPT_Data+2
	lda EXCEPT_Next
	sta !TH_Except,x
	ldx EXCEPT_Stack
	lda EXCEPT_RetAdd
	sta @3,x
	lda EXCEPT_RetAdd+2
	sta @5,x
	lda EXCEPT_DP
	tcd
	inx
	inx
	txs
	plb
	ldx #1
	rtl
	.)
	
popex_S	.(
	phb
	!KER_ENT_DBR
	ldy Current
	ldx !TH_Except,y
	beq havex
	lda @EXCEPT_Next,x
	sta !TH_Except,y
havex	plb
	rtl
	.)
	
debug_S	php
	jmp @break

&kpancode .word 14

&break	.(

	.abs 0
CrashP	.long 4
	.text

	clc
	xce
	!AXL
	phb
	pha
	phx
	phy
	phd
	!KER_ENT_DBR
	tsc
	ldx Current
	sta !TH_Stack,x
	sei
	!AS
	lda #$7f
	sta @CIA2+ICR
	!AL
	lda #$80
	tcs
	lda #2
	tcd
	lda kpancode
	sta @$4004
	ldx CurrentP
	lda !PCB_PID,x
	sta @$4002
	lda #$4006
	sta CrashP
	stz CrashP+2
	jsr debProc
	jsr debMem
	lda CrashP
	sta @$4000
	
.if DEBUGKERN
	ldx #$0000
	ldy #$0000
	lda #$ffff
	mvn $001d
	ldx #$0000
	ldy #$0000
	lda #$ffff
	mvn $011e
.endif

	ldx #$1000
	ldy #$1000
	lda #$5000-1
	mvn $f801 

	ldx #$8000
	ldy #$6000
	lda #$2000-1
	mvn $f801 

	ldx #$a000
	ldy #$a000
	lda #$2000-1
	mvn $f801 

	ldx #$e000
	ldy #$e000
	lda #$2000-1
	mvn $f801 

; fbe0 - fcc4 - f89be0
; fe00 - fe43 - f8070e
; ffe4 - 0000 - f806dd

	ldx #$9be0
	ldy #$fbe0
	lda #$fcc4-$fbe0-1
	mvn $f801 

	ldx #$070e
	ldy #$fe00
	lda #$43-1
	mvn $f801 

	ldx #$06dd
	ldy #$ffe4
	lda #$10000-$ffe4-1
	mvn $f801 
	
	lda #Respoint
	sta @$1a000
	!KER_ENT_DBR
	ldx #0
cop2p1	lda Restart,x
	sta @$1000,x
	inx
	inx
	cpx #ResSize
	bcc cop2p1
	pea 0
	plb
	plb
	jmp @$1003
	
Restart	= *
	*= $1000
	jmp saveit
	!AXS
	lda #$37
	sta @1
	sec
	xce
	jmp $fce2
	
Respoint	= *
.if	PRLINK
	lda $cd00
	cmp #$4c
	bne nopr
	jsr $cd00
nopr
.endif	
	lda #$e3
	sta @$1a001
	lda #$94
	sta @$1a000
	lda #$4c
	sta @$1e39f
	lda #<nextbit
	sta @$1e3a0
	lda #>nextbit
	sta @$1e3a1
	
	lda @kpancode
	sta $d020
	jmp $e394

nextbit	= *
	ldy #>status
	lda #<status
	jsr $ab1e
	lda #$8d
	sta @$1e39f
	lda #<$d07e
	sta @$1e3a0
	lda #>$d07e
	sta @$1e3a1
	ldx #$fb
	jmp $e39f

saveit	lda #12
	ldx #<fname
	ldy #>fname
	jsr $ffbd
	lda #1
	ldx #8
	ldy #0
	jsr $ffba
	lda #$40
	sta $ff
	lda #$00
	sta $fe
	ldx $4000
	ldy $4001
	lda #$fe
	jsr $ffd8
	rts

status	.psc 13,"arghhh!",13
	.psc "please reset drives and type sys 4096",13
	.psc "and email the resulting '"
fname	.psc "wings.report"
	.psc "'",13
	.psc "along with a description of what you",13
	.psc "were doing at the time to:",13
	.psc "jmaginni@primus.com.au",13,0

	.text
ResSize	= * - Restart

	.)	
	.)
	
