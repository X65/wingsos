	
	.text

	/* Widget library */

	.(

	.al
	.xl
	.pic
	
	.include	<kern.i65>
	.include	<win.i65>	
	.include	<stdio.i65>
	.include	<widget.i65>
	.include	<fsys.i65>
	.include	<lcc.i65>

;DEBUG	= 

&_JAppSetMain .(
	.abs 1	
LZ	= *-1
	.dsb 5
Self	.long 4
Main	.long 4
	.text
	
	!PRO LZ
	ldx #{MainWin
	jsr @S_segoff
	tax
	lda Main
	sta MainWin,x
	lda Main+2
	sta MainWin+2,x
	!POST LZ
	rtl
	
	.)
	
&_JAppDrain	.(
back	pea WIN_PollEvent
	ldx #{WCon
	jsr @S_segoff
	tax
	lda WCon,x
	pha
	jsr @_sendCon
	pla
	pla
	cpx #0
	beq out
	jsr @_JEvent
	bra back
out	rtl
	.)
	
&_JAppLoop .(

back	jsr @_JEvent
	bra back

	.)

&_JEvent	
	.(

	.abs 1
LZ	= *-1
	.dsb 5
	.text

	!PRO LZ
	pea 2048
	ldx #{AnEvent
	lda #AnEvent
	jsr @S_segptr
	pha
	phy
	pea WIN_RecvEvent
	ldx #{WCon
	jsr @S_segoff
	tax
	lda WCon,x
	pha
	jsr @_sendCon
	!POP 10
	ldx #{AnEvent
	lda #AnEvent
	jsr @S_segptr
	pha
	phy
	lda !EV_Data+2,y
	pha
	lda !EV_Data,y
	pha
	ldx #MJW_Handle
	jsr @VMC		; VMC to handle
	!POST LZ+8
	rtl

	.)


&_VMC	.(

; C Version of virtual method call

	lda 4,s
	tax
	lda 2,s
	sta 4,s
	lda 1,s
	sta 3,s
	pla
	.)
	
	.(

Self	= 4

&&VMCheck
	lda Self,s
	ora Self+1,s
	beq nocall
; ASM virtual method call

&&VMC	lda Self,s
	ora Self+2,s
	bne oki
	brk
oki	!AS
	lda Self+2,s
	pha
	!AL
	lda Self+1,s
	dec
	pha
nocall	rtl
	
	.)
	
&cb	.(
	txy
	bne notn
	tay
	beq isz
notn	!AS
	pha
	!AL
	dex
	phx
isz	rtl
	.)

&_JAppInit	.(

; Initialise App, open connection to win.drv

self	= 4
channel	= 8

	lda channel,s
	pha
	pea O_PROC
	pea ^winname
	pea !winname
	jsr @_open
	pla
	pla
	pla
	pla
	phx
	ldx #{WCon
	jsr @S_segoff
	tay
	lda 1,s
	sta WCon,y
	ldx #{JRootObj
	jsr @S_segoff
	tay
	pla
	sta JRootObj+JW_Con,y
	cmp #-1
	bne okgood
	pea ^nowin
	pea !nowin
	jsr @_printf
	pla
	pla
	pea 1
	jsr @_exit
okgood	lda #$5c
	sta JRootObj+JObj_VMT,y
	lda #!JWMethods
	sta JRootObj+JObj_VMT+1,y
	!AS
	lda #^JWMethods
	sta JRootObj+JObj_VMT+3,y
	!AL
	lda #320
	sta JRootObj+JW_XSize,y
	lda #200
	sta JRootObj+JW_YSize,y
	lda #0
	sta JRootObj+JW_Parent,y
	sta JRootObj+JW_Parent+2,y
	lda @DflPen
	ora @DflBack
	sta JRootObj+JW_Colours,y
	rtl
	
	.)
	
&_JNew	.(

; Initialise an object to nulls and allocate memory

	.abs 1
Self	.long 4
LZ	= *-1
	.dsb 5
Class	.long 4

	.text
	!PRO LZ
	ldy #JObjClass_ObjSize
	lda [Class],y
	pha
	pea 0
	pha
	jsr @_malloc
	!POP 4
	stx Self
	sty Self+2
	ply
	lda #0
	bra clrit
clrob	sta [Self],y
clrit	dey
	dey
	bpl clrob
	
	ldy #JObjClass_VMT
	lda [Class],y
	sta [Self]
	iny
	iny
	lda [Class],y
	ldy #2
	sta [Self],y
	iny
	iny
	lda Class
	sta [Self],y
	iny
	iny
	lda Class+2
	sta [Self],y
	ldx Self
	ldy Self+2
	!POST LZ
	rtl
	
	.)

/* ----------------------------------------

        Overriding classes
	JSubclass

------------------------------------------*/

&_JSubclass .(

	.abs 1
MethP	.long 4
OldP	.long 4
Offset	= *
MethSz	.word 2
NewClass .long 4
LZ	= *-1
	.dsb 5
Class	.long 4
ObjSize	.word 2
Method	.word 2
Routine	.long 4
	.text

	!PRO LZ
	pea 0
	pea JWClass_SIZE
	jsr @_malloc
	!POP 4
	stx NewClass
	sty NewClass+2
	
	ldy #JObjClass_ObjSize
	lda ObjSize
	cmp #-1
	bne notm1
	lda [Class],y
notm1	sta [NewClass],y
	
	ldy #JObjClass_MethSize
	lda [Class],y
	sta [NewClass],y
	sta MethSz
	
	ldy #JObjClass_Name
	lda [Class],y
	sta [NewClass],y
	iny
	iny
	lda [Class],y
	sta [NewClass],y
	
	lda MethSz
	asl
	adc #16
	pea 0
	pha
	jsr @_balloc
	pla
	pla
	tya
	ldy #JObjClass_VMT+3
	!AS
	sta [NewClass],y
	!AL
	sta MethP+2
	dey
	dey
	txa
	sta [NewClass],y
	sta MethP
	!AS
	lda #$5c
	sta [NewClass]
	!AL
	
	lda #$20e2		; sep #$20
	sta [MethP]
	ldy #2
	lda #$00bf		; lda @,x
	sta [MethP],y
	lda MethSz
	clc
	adc #$10
	adc MethP
	ldy #3
	sta [MethP],y
	lda MethP+2
	adc #0
	iny
	iny
	sta [MethP],y
	ldy #6
	lda #$c248		; pha
	sta [MethP],y	; rep #$20
	ldy #8
	lda #$bf20		; lda @,x
	sta [MethP],y	; 
	lda MethP
	clc
	adc #$10
	ldy #10
	sta [MethP],y
	lda MethP+2
	adc #0
	iny
	iny
	sta [MethP],y
	ldy #13
	lda #$483a		; dec
	sta [MethP],y	; pha
	ldy #15
	lda #$6b		; rtl
	sta [MethP],y
	
	lda #^_JSubclass
	sta OldP+2
	ldx MethSz
	txa
	clc
	adc #$10
	tay
	lda OldP+2
coptop	dex
	dex
	bmi donecop
	sta [MethP],y
	iny
	iny
	bra coptop
donecop	ldy #JObjClass_VMT+1
	lda [Class],y
	clc
	adc #3
	sta OldP
	
	lda MethP
	clc
	adc #$10
	sta MethP
	bcc ninc
	inc MethP+2
ninc	ldy #0
copit2	lda [OldP],y
	sta [MethP],y
	iny
	iny
	cpy MethSz
	bcc copit2
	
	ldx #0
morem	lda Method,x
	inc
	beq donem
	dec
	tay
	lda Routine,x
	sta [MethP],y
	tya
	clc
	adc MethSz
	tay
	lda Routine+2,x
	sta [MethP],y
	txa
	clc
	adc #6
	tax
	bra morem
	
donem	ldx NewClass
	ldy NewClass+2
	!POST LZ
	rtl
	
	.)
	
/*--------------------------------------------
	Standard Widget Methods
--------------------------------------------*/
	
&_JWinInit	.(

; Constructor for base class JWin
; Sets up rectangle and adds it to it's parent
	
	.abs 1
LZ	= *-1
	.dsb 5
Self	.long 4
X	.word 2
Y	.word 2
XSize	.word 2
YSize	.word 2
Parent	.long 4
Sense	.word 2
Flags	.word 2

	.text
	!PRO LZ
	!PRPOBJ JW
	ldx #{WCon
	jsr @S_segoff
	tax
	lda WCon,x
	ldy #JW_Con
	sta [Self],y
	lda #8
	ldy #JW_MinXS
	sta [Self],y
	ldy #JW_MinYS
	sta [Self],y
	lda X
	and #$ffff-7
	ldy #JW_X
	sta [Self],y
	ldy #JW_XG
	sta [Self],y
	lda Y
	and #$ffff-7
	ldy #JW_Y
	sta [Self],y
	ldy #JW_YG
	sta [Self],y
	lda XSize
	and #$ffff-7
	ldy #JW_XSize
	sta [Self],y
	ldy #JW_XSizeG
	sta [Self],y
	lda YSize
	and #$ffff-7
	ldy #JW_YSize
	sta [Self],y
	ldy #JW_YSizeG
	sta [Self],y
	ldy #JW_Flags
	lda Flags
	ora #JF_Resized
	sta [Self],y
	ldy #JW_HideCnt
	lda #1
	sta [Self],y
	ldy #JW_RegFlags
	lda Flags
	tax
	and #JF_Selectable
	beq nosel
	lda #REGF_Selectable
	sta [Self],y
nosel	txa
	and #JF_Front
	beq nofront
	lda [Self],y
	ora #REGF_Front
	sta [Self],y
nofront	txa
	and #JF_Manage
	beq noman
	lda [Self],y
	ora #REGF_Managed
	sta [Self],y
noman	lda Sense
	ora #WEV_Expose
	ldy #JW_Sense
	sta [Self],y
	ldy #JW_Opaque
	lda #WEV_Draw+WEV_Expose
	sta [Self],y
		
	lda Parent
	ora Parent+2
	bne okpare
	ldx #{JRootObj
	lda #JRootObj
	jsr @S_segptr
	sta Parent+2
	sty Parent
okpare	ldy #JW_Colours
	lda [Parent],y
	sta [Self],y
	pei (Self+2)	; Add it to parent
	pei (Self)
	pei (Parent+2)
	pei (Parent)
	ldx #MJW_AddChild
	jsr @VMC
	!POP 8
exeet	ldx Self
	ldy Self+2
	!POST LZ
	rtl
	.)

&_JWinKill	.(

; Destructor for JWin
; First hides it, then kills all children
; And frees the memory
	.abs 1
WinP	.long 4
LZ	= *-1
	.dsb 5
Self	.long 4

	.text
	!PRO LZ
	ldy #JW_RegFlags
	lda [Self],y
	and #$ffff-REGF_Visible
	sta [Self],y
	pha
	pea EREG_Flags
	ldy #JW_RegID
	lda [Self],y
	pha
	pea WIN_EditRegion
	ldy #JW_Con
	lda [Self],y
	pha
	jsr @_sendCon
	!POP 10
	ldy #JW_BackCh
	lda [Self],y
	sta WinP
	iny
	iny
	lda [Self],y
	sta WinP+2
gochild	lda WinP
	ora WinP+2
	beq nomore
	pei (WinP+2)
	pei (WinP)
	ldx #MJW_Kill
	jsr @VMC		; VMC to Kill
	pla
	pla
	ldy #JW_Next
	lda [WinP],y
	tax
	iny
	iny
	lda [WinP],y
	sta WinP+2
	stx WinP
	bra gochild
	
nomore	pei (Self+2)
	pei (Self)
	jsr @_JWinRem
	pla
	pla
	
	ldy #JW_RegID
	lda [Self],y
	pha
	pea WIN_DelRegion
	ldy #JW_Con
	lda [Self],y
	pha
	jsr @_sendCon
	pla
	pla
	pla
	pei (Self+2)
	pei (Self)
	jsr @_free
	!POST LZ+4
	rtl

	.)

&_JWinRem	
	.(

; Remove a child from it's parents list
	.abs 1
NextP	.long 4
PrevP	.long 4
LZ	= *-1
	.dsb 5

Self	.long 4
	.text

	!PRO LZ
	ldy #JW_Parent
	lda [Self],y
	sta NextP
	iny
	iny
	lda [Self],y
	sta NextP+2
	
	pea CHNOTE_Removed
	pei (Self+2)
	pei (Self)
	pei (NextP+2)
	pei (NextP)
	ldx #MJW_ChNotice
	jsr @VMC
	!POP 10
	
	ldy #JW_BackCh
	lda [NextP],y
	cmp Self
	bne notback
	iny
	iny
	lda [NextP],y
	cmp Self+2
	bne notback
	ldy #JW_Next
	lda [Self],y
	ldy #JW_BackCh
	sta [NextP],y
	ldy #JW_Next+2
	lda [Self],y
	ldy #JW_BackCh+2
	sta [NextP],y
notback	ldy #JW_FrontCh
	lda [NextP],y
	cmp Self
	bne notfront
	ldy #JW_FrontCh+2
	lda [NextP],y
	cmp Self+2
	bne notfront
	ldy #JW_Prev
	lda [Self],y
	ldy #JW_FrontCh
	sta [NextP],y
	ldy #JW_Prev+2
	lda [Self],y
	ldy #JW_FrontCh+2
	sta [NextP],y
notfront	ldy #JW_Focused
	lda [NextP],y
	cmp Self
	bne notsel
	ldy #JW_Focused+2
	lda [NextP],y
	cmp Self+2
	bne notsel
	lda #0
	sta [NextP],y
	ldy #JW_Focused
	sta [NextP],y
notsel	ldy #JW_Prev
	lda [Self],y
	sta PrevP
	ldy #JW_Prev+2
	lda [Self],y
	sta PrevP+2
	ldy #JW_Next
	lda [Self],y
	sta NextP
	ldy #JW_Next+2
	lda [Self],y
	sta NextP+2
	lda PrevP
	ora PrevP+2
	beq nobeh
	ldy #JW_Next
	lda NextP
	sta [PrevP],y
	ldy #JW_Next+2
	lda NextP+2
	sta [PrevP],y
nobeh	lda NextP
	ora NextP+2
	beq nofro
	ldy #JW_Prev
	lda PrevP
	sta [NextP],y
	ldy #JW_Prev+2
	lda PrevP+2
	sta [NextP],y
nofro	!POST LZ
	rtl

	.)
	
&_JWinAdd	.(

; Add a child
; Creates the region (Unless REGF_InParent)
; Adds it to the front
	.abs 1
NextP	.long 4
Region	.dsb REG_SIZE
LZ	= *-1
	.dsb 5
Self	.long 4
WinP	.long 4
	.text

	!PRO LZ
	ldy #JW_Flags
	lda [WinP],y
	pha
	and #JF_Fixed
	tax
	pla
	and #JF_InParent
	!jne notreg
	ldy #JW_X
	lda [WinP],y
	cpx #0
	bne isf
	sec
	ldy #JW_XScrld
	sbc [Self],y
isf	sta Region+REG_X
	ldy #JW_Y
	lda [WinP],y
	cpx #0
	bne isf2
	sec
	ldy #JW_YScrld
	sbc [Self],y
isf2	sta Region+REG_Y
	ldy #JW_XSize
	lda [WinP],y
	sta Region+REG_XSize
	ldy #JW_YSize
	lda [WinP],y
	sta Region+REG_YSize
	ldy #JW_RegID
	lda [Self],y
	sta Region+REG_Parent
	ldy #JW_Sense
	lda [WinP],y
	sta Region+REG_Sense
	ldy #JW_Opaque
	lda [WinP],y
	sta Region+REG_Opaque
	ldy #JW_RegFlags
	lda [WinP],y
	sta Region+REG_Flags		; TODO
	lda WinP
	sta Region+REG_Data
	lda WinP+2
	sta Region+REG_Data+2
	pea 0
	tdc
	clc
	adc #Region
	pha
	pea WIN_AddRegion
	ldy #JW_Con
	lda [Self],y
	pha
	jsr @_sendCon
	tsc
	clc
	adc #8
	tcs
	txa
	ldy #JW_RegID
	sta [WinP],y
	ldy #JW_Flags
	lda #JF_Added
	ora [WinP],y
	sta [WinP],y
	bra donereg

notreg	ldy #JW_RegID
	lda [Self],y
	sta [WinP],y
	ldy #JW_HasCh
	lda #1
	sta [Self],y

donereg	ldy #JW_Parent
	lda Self
	sta [WinP],y
	iny
	iny
	lda Self+2
	sta [WinP],y
	ldy #JW_TopLevel
	lda [Self],y
	tax
	iny
	iny
	lda [Self],y
	bne gotop
	cpx #0
	bne gotop
	ldx WinP
	lda WinP+2
gotop	sta [WinP],y
	txa
	ldy #JW_TopLevel
	sta [WinP],y
	
	/* Add to front of children */
	
	ldy #JW_FrontCh
	lda [Self],y
	sta NextP
	ldy #JW_Prev
	sta [WinP],y
	ldy #JW_FrontCh+2
	lda [Self],y
	sta NextP+2
	ldy #JW_Prev+2
	sta [WinP],y
	
	lda WinP
	ldy #JW_FrontCh
	sta [Self],y
	lda WinP+2
	iny
	iny
	sta [Self],y
	
	lda NextP
	ora NextP+2
	beq nonex
	lda WinP
	ldy #JW_Next
	sta [NextP],y
	lda WinP+2
	iny
	iny
	sta [NextP],y
	bra skinex
nonex	lda WinP
	ldy #JW_BackCh
	sta [Self],y
	lda WinP+2
	iny
	iny
	sta [Self],y

skinex	ldy #JW_Flags
	lda [WinP],y
	and #JF_Selectable
	beq nosel
	pea 1
	pei (WinP+2)
	pei (WinP)
	ldx #MJW_Select
	jsr @VMC
	pla
	pla
	pla
nosel	pea CHNOTE_Added
	pei (WinP+2)
	pei (WinP)
	pei (Self+2)
	pei (Self)
	ldx #MJW_ChNotice
	jsr @VMC
	!POST LZ+10
	rtl
	.)

&_JRegInfo	.(

; Get region info
	.abs 1
LZ	= *-1
	.dsb 5

Region	.word 2
BufP	.long 4

	.text
	!PRO LZ
	pei (BufP+2)
	pei (BufP)
	pei (Region)
	pea WIN_InfoRegion
	ldx #{WCon
	jsr @S_segoff
	tax
	lda WCon,x
	pha
	jsr @_sendCon
	!POST 10+LZ
	rtl

	.)

	
&_JWinReq	.(

; Ask to be the "Window Manager"
	.abs 1
LZ	= *-1
	.dsb 5

Self	.long 4
	.text

	!PRO LZ
	ldy #JW_RegID
	lda [Self],y
	pha
	pea WIN_ReqNotify
	ldy #JW_Con
	lda [Self],y
	pha
	jsr @_sendCon
	!POST LZ+6
	rtl

	.)
	
&_JWinRePare .(

; Reparent Win to another region
	.abs 1
LZ	= *-1
	.dsb 5

Self	.long 4
RegID	.word 2
	.text

	!PRO LZ
	ldy #JW_RegID
	lda [Self],y
	pha
	pei (RegID)
	pea WIN_Reparent
	ldy #JW_Con
	lda [Self],y
	pha
	jsr @_sendCon
	!POST LZ+8
	rtl

	.)

&_JWinAbs .(

; Find out the absolute co-ordiantes of this Win

	.abs 1
LZ	= *-1
	.dsb 5
	
Self	.long 4
XY	.long 4
	.text
	
	!PRO LZ
	pei (XY+2)
	pei (XY)
	ldy #JW_RegID
	lda [Self],y
	pha
	pea WIN_RegAbs
	ldy #JW_Con
	lda [Self],y
	pha
	jsr @_sendCon
	!POST LZ+10
	rtl

	.)

&_JWinMouFoc .(
	.abs 1
LZ	= *-1
	.dsb 5
Self	.long 4
	.text
	
	!PRO LZ
	ldy #JW_RegID
	lda [Self],y
	pha
	pea WIN_MouseFocus
	ldy #JW_Con
	lda [Self],y
	pha
	jsr @_sendCon
	!POST LZ+6
	rtl
	
	.)	

&_JWinOpaque .(

	.abs 1	
LZ	= *-1
	.dsb 5

Self	.long 4
Flags	.word 2
On	.word 2
	.text

	!PRO LZ
	ldy #JW_Opaque
	lda On
	beq off
	lda Flags
	ora [Self],y
	bra chsense
off	lda Flags
	eor #-1
	and [Self],y
chsense	cmp [Self],y
	beq exeet
	sta [Self],y
	pha
	pea EREG_Opaque
	ldy #JW_RegID
	lda [Self],y
	pha
	pea WIN_EditRegion
	ldy #JW_Con
	lda [Self],y
	pha
	jsr @_sendCon
	!POP 10
exeet	!POST LZ
	rtl
	
	.)	

&_JWinSense	.(
	
	.abs 1
LZ	= *-1
	.dsb 5

Self	.long 4
Flags	.word 2
On	.word 2
	.text

	!PRO LZ
	ldy #JW_Sense
	lda On
	beq off
	lda Flags
	ora [Self],y
	bra chsense
off	lda Flags
	eor #-1
	and [Self],y
chsense	cmp [Self],y
	beq exeet
	sta [Self],y
	pha
	pea EREG_Sense
	ldy #JW_RegID
	lda [Self],y
	pha
	pea WIN_EditRegion
	ldy #JW_Con
	lda [Self],y
	pha
	jsr @_sendCon
	!POP 10
exeet	!POST LZ
	rtl
	
	.)	

&_JEGeom	.(

	.abs 1
LZ	= *-1
	.dsb 5
	
Region	.word 2
X	.word 2
Y	.word 2
XSize	.word 2
YSize	.word 2
	.text

	!PRO LZ
	pei (YSize)
	pei (XSize)
	pei (Y)
	pei (X)
	pea EREG_SizePos
	pei (Region)
	pea WIN_EditRegion
	ldx #{WCon
	jsr @S_segoff
	tax
	lda WCon,x
	pha
	jsr @_sendCon
	!POST LZ+16
	rtl
	
	.)

&_JShow	.(
	.abs 1
LZ	= *-1
	.dsb 5
	
Region	.word 2
	.text

	!PRO LZ
	pea REGF_Visible
	pea EREG_Flags
	pei (Region)
	pea WIN_EditRegion
	ldx #{WCon
	jsr @S_segoff
	tax
	lda WCon,x
	pha
	jsr @_sendCon
	!POST LZ+10
	rtl

	.)
		
&_JWinShow	.(
	.abs 1
WinP	.long 4
LZ	= *-1
	.dsb 5

Self	.long 4
	.text

	!PRO LZ
	ldy #JW_BackCh
	lda [Self],y
	sta WinP
	iny
	iny
	lda [Self],y
	sta WinP+2
gochild	lda WinP
	ora WinP+2
	beq nomore
	pei (WinP+2)
	pei (WinP)
	ldx #MJW_Show
	jsr @VMC		; VMC to Show
	pla
	pla
	ldy #JW_Next
	lda [WinP],y
	tax
	ldy #JW_Next+2
	lda [WinP],y
	sta WinP+2
	stx WinP
	bra gochild
nomore	ldy #JW_HideCnt
	lda [Self],y
	dec
	sta [Self],y
	bne nodraw
	ldy #JW_Flags
	lda [Self],y
	and #JF_Added
	bne isadded
	pei (Self+2)
	pei (Self)
	jsr @_JWinReDraw
	pla
	pla
	bra nodraw
isadded	ldy #JW_RegFlags
	lda [Self],y
	ora #REGF_Visible
	pha
	pea EREG_Flags
	ldy #JW_RegID
	lda [Self],y
	pha
	pea WIN_EditRegion
	ldy #JW_Con
	lda [Self],y
	pha
	jsr @_sendCon
	pla
	pla
	pla
	pla
	pla
	cpx #1
	bne nodraw
	ldy #JW_RegFlags
	lda [Self],y
	ora #REGF_Visible
	sta [Self],y
nodraw	!POST LZ
	rtl
	
	.)	

&_JWinHide	.(

	.abs 1	
WinP	.long 4
LZ	= *-1
	.dsb 5

Self	.long 4
	.text
	
	!PRO LZ
	ldy #JW_BackCh
	lda [Self],y
	sta WinP
	iny
	iny
	lda [Self],y
	sta WinP+2
gochild	lda WinP
	ora WinP+2
	beq nomore
	pei (WinP+2)
	pei (WinP)
	jsr @_JWinHide
	pla
	pla
	ldy #JW_Next
	lda [WinP],y
	tax
	iny
	iny
	lda [WinP],y
	sta WinP+2
	stx WinP
	bra gochild
nomore	ldy #JW_HideCnt
	lda [Self],y
	bne fine
	ldy #JW_Flags
	lda [Self],y
	and #JF_Added
	bne isadd
	pei (Self+2)
	pei (Self)
	jsr @_JWinReDraw
	pla
	pla
	bra didred
isadd	ldy #JW_RegFlags
	lda [Self],y
	and #$ffff-REGF_Visible
	sta [Self],y
	pha
	pea EREG_Flags
	ldy #JW_RegID
	lda [Self],y
	pha
	pea WIN_EditRegion
	ldy #JW_Con
	lda [Self],y
	pha
	jsr @_sendCon
	tsc
	clc
	adc #10
	tcs
didred	ldy #JW_HideCnt
	lda [Self],y
fine	inc
	sta [Self],y
	!POST LZ
	rtl
	
	.)	

&_JWinSetPen .(

	.abs 1
LZ	= *-1
	.dsb 5

Self	.long 4
PenCol	.word 2
	
	.text
	!PRO LZ
	lda PenCol
	asl
	asl
	asl
	asl
	and #$f0
	pha
	ldy #JW_Colours
	lda [Self],y
	and #$0f
	ora 1,s
	plx
	sta [Self],y
	!POST LZ
	rtl

	.)
			
&_JWinSetBack .(

	.abs 1
LZ	= *-1
	.dsb 5

Self	.long 4
BackCol	.word 2
	.text

	!PRO LZ
	lda BackCol
	and #$0f
	pha
	ldy #JW_Colours
	lda [Self],y
	and #$f0
	ora 1,s
	plx
	sta [Self],y
	!POST LZ
	rtl

	.)
	
&_JWinHandle .(

	.abs 1	
RectP	.long 4
TopP	.long 4
DataP	.long 4
WinP	.long 4
Temp	.word 2
TX	.word 2
TY	.word 2
LZ	= *-1
	.dsb 5
Self	.long 4
EveP	.long 4

	.text
	!PRO LZ
	lda EveP
	clc
	adc #EV_SIZE
	sta DataP
	lda EveP+2
	adc #0
	sta DataP+2
	ldy #EV_DataSz
	lda [EveP],y
	clc
	adc DataP
	sta RectP
	lda DataP+2
	adc #0
	sta RectP+2
	ldy #EV_Type
	lda [EveP],y
	cmp #WEV_Focus
	bne nofoc
	ldy #EV_SubType
	lda [EveP],y
	pha
	pei (Self+2)
	pei (Self)
	ldx #MJW_Focus		; Focus Changed
	jsr @VMC
	pla
	pla
	pla
	brl exeet
nofoc	cmp #WEV_Button
	bne nobut
	ldy #EV_SubType
	lda [EveP],y
	pha
	and #EVS_ButsDown
	beq nobd
	ldy #JW_Flags
	lda [Self],y
	and #JF_Selectable|JF_Selected
	cmp #JF_Selectable
	bne nahsel
	pea 1
	pei (Self+2)
	pei (Self)
	ldx #MJW_Select
	jsr @VMC
	pla
	pla
	pla	
nahsel	ldy #JW_TopLevel
	lda [Self],y
	sta TopP
	iny
	iny
	lda [Self],y
	sta TopP+2
	ldy #JW_Flags
	lda [TopP],y
	and #JF_Focused+JF_Selectable
	cmp #JF_Selectable
	bne nobd
	ldy #JW_RegID
	lda [TopP],y
	pha
	pea WIN_KeyFocus
	ldy #JW_Con
	lda [Self],y
	pha
	jsr @_sendCon
	pla
	pla
	pla
nobd	ldx #MJW_Button
	pla
	and #EVS_But2Mask
	beq b2mask
	ldx #MJW_RButton
b2mask	ldy #EV_SubType
	bra mouseev

nobut	cmp #WEV_Keyboard
	bne nokey
	ldy #EV_SIZE		; No rects...
	lda [EveP],y
	pha
	pei (Self+2)
	pei (Self)
	ldx #MJW_KeyDown
	jsr @VMC
	pla
	pla
	pla
	brl exeet

nokey	cmp #WEV_Motion
	beq ismot
	cmp #WEV_MotionBut
	beq ismot
	brl nomot
	
ismot	ldx #MJW_Motion
	ldy #EV_Type
	
mouseev	sty Temp
	ldy #EV_SIZE+2
	lda [EveP],y
	sec
	ldy #EV_TransY
	sbc [EveP],y
	pha
	
	ldy #EV_SIZE
	lda [EveP],y
	sec
	ldy #EV_TransX
	sbc [EveP],y	
	pha
	
	ldy #EV_SIZE+2
	lda [EveP],y
	pha
	ldy #EV_SIZE
	lda [EveP],y
	pha
	ldy Temp
	lda [EveP],y
	pha
	phx
tryag	ldy #JW_HasCh
	lda [Self],y
	bne ischi
	brl nochi
ischi	ldy #JW_FrontCh
	lda [Self],y
	sta WinP
	iny
	iny
	lda [Self],y
	sta WinP+2
	lda 5,s
	clc
	ldy #JW_XScrld
	adc [Self],y
	sta 5,s
	lda 7,s
	clc
	ldy #JW_YScrld
	adc [Self],y
	sta 7,s
nxchild	lda WinP
	ora WinP+2
	beq nochi
	ldy #JW_Flags
	lda [WinP],y
	and #JF_Added
	bne nope
	ldy #JW_X
	lda [WinP],y
	cmp 5,s
	beq ok1
	bpl nope
ok1	ldy #JW_XSize
	clc
	adc [WinP],y
	cmp 5,s
	beq nope
	bmi nope
	ldy #JW_Y
	lda [WinP],y
	cmp 7,s
	beq ok2
	bpl nope
ok2	ldy #JW_YSize
	clc
	adc [WinP],y
	cmp 7,s
	beq nope
	bmi nope
	ldy #JW_X
	lda 5,s
	sec
	sbc [WinP],y
	sta 5,s
	ldy #JW_Y
	lda 7,s
	sec
	sbc [WinP],y
	sta 7,s
	lda WinP
	sta Self
	lda WinP+2
	sta Self+2
	brl tryag
nope	ldy #JW_Prev
	lda [WinP],y
	tax
	ldy #JW_Prev+2
	lda [WinP],y
	sta WinP+2
	stx WinP
	bra nxchild
nochi	plx
	pei (Self+2)
	pei (Self)
	jsr @VMC
	!POP 14
	bra exeet

nomot	cmp #WEV_Boundary
	bne nobou
	ldy #EV_SubType
	lda [EveP],y
	pha
	pei (Self+2)
	pei (Self)
	ldx #MJW_Bound
	jsr @VMC
	pla
	pla
	pla
	bra exeet
nobou	cmp #WEV_Notice
	bne nonot
	pei (DataP+2)
	pei (DataP)
	ldy #EV_Sender
	lda [EveP],y
	pha
	ldy #EV_SubType
	lda [EveP],y
	pha
	pei (Self+2)
	pei (Self)
	ldx #MJW_Notice
	jsr @VMC
	!POP 12
	bra exeet
nonot	cmp #WEV_Expose
	beq doexp
exeet	!POST LZ
	rtl

doexp	ldy #JW_HideCnt
	lda [Self],y
	bne exeet
	ldx #{P_XOffs
	jsr @S_segoff
	tax
	stz P_XOffs,x
	stz P_YOffs,x
	stz P_HasClear,x
	stz P_PenX,x
	stz P_PenY,x
	lda #PNT_BUFSIZE
	sta P_BufLeft,x
	txa
	sta P_BufIn,x
	stz P_BufDone,x

	ldy #JW_RegID
	lda [Self],y
	sta P_Ev+EV_Sender,x
	
	lda RectP
	sta P_Rects,x 
	lda RectP+2
	sta P_Rects+2,x 
	ldy #EV_NumRects
	lda [EveP],y
	sta P_Ev+EV_NumRects,x
	cmp #20
	bcc less40
	lda #1
	sta P_Ev+EV_NumRects,x
	lda #0
	sta [RectP]
	ldy #2
	sta [RectP],y
	sta _ClipRect,x
	sta _ClipRect+2,x
	ldy #JW_XSize
	lda [Self],y
	ldy #4
	sta [RectP],y
	sta _ClipRect+4,x
	ldy #JW_YSize
	lda [Self],y
	ldy #6
	sta [RectP],y
	sta _ClipRect+6,x
	brl dopaint
less40	sta Temp
	lda #-1
	sta _ClipRect,x
	sta _ClipRect+2,x
	stz _ClipRect+4,x
	stz _ClipRect+6,x
	stz TX
	stz TY
	ldy #JW_Flags
	lda [Self],y
	and #JF_Fixed
	bne isfx
	ldy #JW_Parent
	lda [Self],y
	sta WinP
	iny
	iny
	lda [Self],y
	sta WinP+2
	ldy #JW_XScrld
	lda [WinP],y
	sta TX
	ldy #JW_YScrld
	lda [WinP],y
	sta TY
isfx	ldy #EV_TransX
	lda [EveP],y
	sec
	ldy #JW_X
	sbc [Self],y
	clc
	adc TX
	sta TX
	ldy #EV_TransY
	lda [EveP],y
	sec
	ldy #JW_Y
	sbc [Self],y
	clc
	adc TY
	sta TY
	
tranrect	lda TX
	clc
	adc [RectP]
	sta [RectP]
	cmp _ClipRect,x
	bcs ishi1
	sta _ClipRect,x
ishi1	ldy #4
	clc
	adc [RectP],y
	cmp _ClipRect+4,x
	bcc islw1
	sta _ClipRect+4,x
islw1	lda TY
	ldy #2
	clc
	adc [RectP],y
	sta [RectP],y
	cmp _ClipRect+2,x
	bcs ishi2
	sta _ClipRect+2,x
ishi2	ldy #6
	clc
	adc [RectP],y
	cmp _ClipRect+6,x
	bcc islw2
	sta _ClipRect+6,x
islw2	lda RectP
	clc
	adc #RECT_SIZE
	sta RectP
	bcc noinc
	inc RectP+2
noinc	dec Temp
	bne tranrect
dopaint	pei (Self+2)
	pei (Self)
	jsr drawAll
	pla
	pla
	jsr @_GfxFlush
	brl exeet
	.)
	
drawAll	.(
	.abs 1
WinP	.long 4
Rect2	.word 2
LZ	= *-1
	.dsb 4

Self	.long 4
	.text

	!PRO LZ
	ldx #{P_Font
	jsr @S_segoff
	tax
	ldy #JW_Font
	lda [Self],y
	sta P_Font,x
	ldy #JW_Colours
	lda [Self],y
	sta P_Col,x
	stz P_Mode,x
	ldy #JW_FStyle
	lda [Self],y
	sta P_Style,x
	jsr @GfxPrep
	pei (Self+2)
	pei (Self)
	ldx #MJW_Draw
	jsr @VMC
	pla
	pla
	ldy #JW_HasCh
	lda [Self],y
	beq nmore2
	ldy #JW_BackCh
	lda [Self],y
	sta WinP
	ldy #JW_BackCh+2
	lda [Self],y
	sta WinP+2
gochild	lda WinP
	ora WinP+2
	bne hasmore
nmore2	brl nomore
hasmore	ldy #JW_RegID
	lda [Self],y
	cmp [WinP],y
	bne nothis
	ldx #{P_YOffs
	jsr @S_segoff
	tax
	lda P_YOffs,x
	pha
	ldy #JW_Y
	clc
	adc [WinP],y
	ldy #JW_YScrld
	sec
	sbc [Self],y
	sta P_YOffs,x
	sta P_PenY,x
	ldy #JW_YSize
	clc
	adc [WinP],y
	sta Rect2
	lda P_XOffs,x
	pha
	ldy #JW_X
	clc
	adc [WinP],y
	ldy #JW_XScrld
	sec
	sbc [Self],y
	sta P_XOffs,x
	sta P_PenX,x
	ldy #JW_XSize
	clc
	adc [WinP],y
	cmp _ClipRect,x
	bmi isclipped
	lda _ClipRect+2,x
	cmp Rect2
	bpl isclipped
	lda _ClipRect+4,x
	cmp P_XOffs,x
	bmi isclipped
	lda _ClipRect+6,x
	cmp P_YOffs,x
	bmi isclipped
	pei (WinP+2)
	pei (WinP)
	jsr drawAll
	pla
	pla
isclipped	ldx #{P_XOffs
	jsr @S_segoff
	tax
	pla
	sta P_XOffs,x
	pla
	sta P_YOffs,x
nothis	ldy #JW_Next
	lda [WinP],y
	tax
	iny
	iny
	lda [WinP],y
	sta WinP+2
	stx WinP
	brl gochild	
nomore	!POST LZ
	rts
	
	.)
	
&_JWinSetData .(

	.abs 1
LZ	= *-1
	.dsb 5

Self	.long 4
Data	.long 4
	.text

	!PRO LZ
	lda Data
	ldy #JW_Data
	sta [Self],y
	lda Data+2
	ldy #JW_Data+2
	sta [Self],y
	!POST LZ
	rtl
	
	.)
	
&_JWinGetData .(

	.abs 1
LZ	= *-1
	.dsb 5

Self	.long 4

	.text
	!PRO LZ
	ldy #JW_Data
	lda [Self],y
	tax
	ldy #JW_Data+2
	lda [Self],y
	tay
	!POST LZ
	rtl
	
	.)

&_JWinSize	.(

	.abs 1
LZ	= *-1
	.dsb 5

Self	.long 4
XSize	.word 2
YSize	.word 2

	.text
	!PRO LZ
	ldy #JW_Anchors
	lda [Self],y
	and #3
	pha
	pei (YSize)
	pei (XSize)
	ldy #JW_YG
	lda [Self],y
	pha
	ldy #JW_XG
	lda [Self],y
	pha
	pei (Self+2)
	pei (Self)
	ldx #MJW_Geom
	jsr @VMC		; VMC to Geom
	!POP 14
exeet	!POST LZ
	rtl

	.)
	

&_JWinMove	.(

	.abs 1
X3	.word 2
Y3	.word 2
PareP	.long 4
LZ	= *-1
	.dsb 5

Self	.long 4
X	.word 2
Y	.word 2
kind	.word 2

	.text
	!PRO LZ
	ldy #JW_Parent
	lda [Self],y
	sta PareP
	iny
	iny
	lda [Self],y
	sta PareP+2
	lda X
	and #$ffff-7
	sta X
	ldy #JW_XG
	sta [Self],y
	lda Y
	and #$ffff-7
	sta Y
	ldy #JW_YG
	sta [Self],y
	ldy #JW_XSize
	lda [PareP],y
	sta X3
	ldy #JW_YSize
	lda [PareP],y
	sta Y3
	lda kind
	and #GEOM_P1Horiz
	beq corx
	lda X3
	sec
	sbc X
	sta X
corx	lda kind
	and #GEOM_P1Vert
	beq cory
	lda Y3
	sec
	sbc Y
	sta Y
cory	lda kind
	ldy #JW_Anchors
	sta [Self],y
	ldy #JW_XSize
	lda [Self],y
	ldy #JW_XSizeG
	sta [Self],y
	ldy #JW_YSize
	lda [Self],y
	ldy #JW_YSizeG
	sta [Self],y
noanch	lda X
	ldy #JW_X
	cmp [Self],y
	bne difpos
	lda Y
	ldy #JW_Y
	cmp [Self],y
	bne difpos
	brl exeet
difpos	pea 1
	ldy #JW_YSize
	lda [Self],y
	pha
	ldy #JW_XSize
	lda [Self],y
	pha
	pei (Y)
	pei (X)
	pei (Self+2)
	pei (Self)
	jsr @_JWinEGeom
	!POP 14
exeet	!POST LZ
	rtl
	
	.)	

&_JWinEGeom	.(

	.abs 1
PareP	.long 4
LZ	= *-1
	.dsb 5

Self	.long 4
X	.word 2
Y	.word 2
XSize	.word 2
YSize	.word 2
Emit	.word 2

	.text
	
	!PRO LZ
	ldy #JW_Parent
	lda [Self],y
	sta PareP
	iny
	iny
	lda [Self],y
	sta PareP+2
	ldy #JW_Flags
	lda [Self],y
	pha
	and #JF_Fixed
	tax
	pla
	and #JF_Added
	beq noadded
	pei (YSize)
	pei (XSize)
	lda Y
	txy
	bne isf
	sec
	ldy #JW_YScrld
	sbc [PareP],y
isf	pha
	lda X
	txy
	bne isf2
	sec
	ldy #JW_XScrld
	sbc [PareP],y
isf2	pha
	pea EREG_SizePos
	ldy #JW_RegID
	lda [Self],y
	pha
	pea WIN_EditRegion
	ldy #JW_Con
	lda [Self],y
	pha
	jsr @_sendCon
	!POP 16
	txa
	beq exeet
	bra nowch
noadded	pei (Self+2)
	pei (Self)
	jsr @_JWinReDraw
	pla
	pla
nowch	lda X
	ldy #JW_X
	sta [Self],y
	lda Y
	ldy #JW_Y
	sta [Self],y
	lda XSize
	ldy #JW_XSize
	sta [Self],y
	lda YSize
	ldy #JW_YSize
	sta [Self],y
	lda Emit
	beq noemit
	pea CHNOTE_Bounds
	pei (Self+2)
	pei (Self)
	pei (PareP+2)
	pei (PareP)
	ldx #MJW_ChNotice
	jsr @VMC
	!POP 10
noemit	ldy #JW_Flags
	lda [Self],y
	and #JF_Added
	bne fine
	pei (Self+2)
	pei (Self)
	jsr @_JWinReDraw
	pla
	pla
fine	ldx #1
exeet	!POST LZ
	rtl
	
	.)

	
&_JWinResize .(

	.abs 1
WinP	.long 4
LZ	= *-1
	.dsb 5
Self	.long 4

	.text

	!PRO LZ
	pei (Self+2)
	pei (Self)
	jsr @_JWinReDraw
	pla
	pla
	ldy #JW_BackCh
	lda [Self],y
	sta WinP
	iny
	iny
	lda [Self],y
	sta WinP+2
gochild	lda WinP
	ora WinP+2
	beq nomore
	ldy #JW_Anchors
	lda [WinP],y
	pha
	ldy #JW_YSizeG
	lda [WinP],y
	pha
	ldy #JW_XSizeG
	lda [WinP],y
	pha
	ldy #JW_YG
	lda [WinP],y
	pha
	ldy #JW_XG
	lda [WinP],y
	pha
	pei (WinP+2)
	pei (WinP)
	ldx #MJW_Geom
	jsr @VMC		; VMC to Geom
	!POP 14
	ldy #JW_Next
	lda [WinP],y
	tax
	iny
	iny
	lda [WinP],y
	sta WinP+2
	stx WinP
	bra gochild
nomore	!POST LZ
	rtl
	
	.)

&_JWinGeom	.(

	.abs 1
PareP	.long 4
X3	.word 2
Y3	.word 2
LZ	= *-1
	.dsb 5
Self	.long 4
X	.word 2
Y	.word 2
X2	.word 2
Y2	.word 2
kind	.word 2
	.text

	!PRO LZ
	lda X
	and #$ffff-7
	sta X
	ldy #JW_XG
	sta [Self],y
	lda Y
	and #$ffff-7
	sta Y
	ldy #JW_YG
	sta [Self],y
	lda X2
	and #$ffff-7
	sta X2
	ldy #JW_XSizeG
	sta [Self],y
	lda Y2
	and #$ffff-7
	sta Y2
	ldy #JW_YSizeG
	sta [Self],y
	lda kind
	ldy #JW_Anchors
	sta [Self],y
	ldy #JW_Parent
	lda [Self],y
	sta PareP
	iny
	iny
	lda [Self],y
	sta PareP+2
	ldy #JW_XSize
	lda [PareP],y
	sta X3
	ldy #JW_YSize
	lda [PareP],y
	sta Y3
	lda kind
	and #GEOM_P1Horiz
	beq corx
	lda X3
	sec
	sbc X
	sta X
corx	lda kind
	and #GEOM_P1Vert
	beq cory
	lda Y3
	sec
	sbc Y
	sta Y
cory	lda kind
	and #GEOM_P2Horiz
	beq corx2
	lda X3
	sec
	sbc X
	sec
	sbc X2
	sta X2
corx2	lda kind
	and #GEOM_P2Vert
	beq cory2
	lda Y3
	sec
	sbc Y
	sec
	sbc Y2
	sta Y2
cory2	lda X2
	ldy #JW_MinXS
	cmp [Self],y
	bpl okxs
	lda [Self],y
	sta X2
okxs	lda Y2
	ldy #JW_MinYS
	cmp [Self],y
	bpl okys
	lda [Self],y
	sta Y2
okys	lda X
	ldy #JW_X
	cmp [Self],y
	bne hasdif
	lda Y
	ldy #JW_Y
	cmp [Self],y
	bne hasdif
	lda X2
	ldy #JW_XSize
	cmp [Self],y
	bne hasdif
	lda Y2
	ldy #JW_YSize
	cmp [Self],y
	bne hasdif
	ldx #0
	brl exeet
hasdif	pea 1
	pei (Y2)
	pei (X2)
	pei (Y)
	pei (X)
	pei (Self+2)
	pei (Self)
	jsr @_JWinEGeom
	tsc
	clc
	adc #14
	tcs
	txa
	beq exeet
	pei (Self+2)
	pei (Self)
	ldx #MJW_Resize
	jsr @VMC
	pla
	pla
	ldx #1
exeet	!POST LZ
	rtl

	.)

/* ----------------------------------------

          Selecting and focusing 

	JWinKeyD
	JWinSelect
	JWinFocus

------------------------------------------*/

&_JWinKeyD	.(

; A key has been pressed
; Pass it to the selected child
	.abs 1	
LZ	= *-1
	.dsb 5

Self	.long 4
Key	.word 2

	.text
	!PRO LZ
	ldy #JW_Focused
	lda [Self],y
	iny
	iny
	ora [Self],y
	beq nosel
	pei (Key)
	lda [Self],y
	pha
	ldy #JW_Focused
	lda [Self],y
	pha
	ldx #MJW_KeyDown
	jsr @VMC
	!POP 6
nosel	!POST LZ
	rtl

	.)
	
&_JWinFocus	.(

	.abs 1
LZ	= *-1
	.dsb 5

Self	.long 4
Type	.word 2

	.text
	!PRO LZ
	ldy #JW_Flags
	lda Type
	cmp #EVS_Gained
	beq gained
	lda [Self],y
	and #-1-JF_Focused
	bra donef
gained	lda [Self],y
	ora #JF_Focused
donef	sta [Self],y
	ldy #JW_Focused
	lda [Self],y
	iny
	iny
	ora [Self],y
	beq nosel
	pei (Type)		; Better tell our selected 
	lda [Self],y		; Child about the focus
	pha
	ldy #JW_Focused
	lda [Self],y
	pha
	ldx #MJW_Focus
	jsr @VMC
	!POP 6
nosel	pei (Self+2)
	pei (Self)
	jsr @_JWinReDraw
	pla
	pla
	!POST LZ
	rtl
	
	.)

&_JWinSelect .(

	.abs 1
PareP	.long 4
LZ	= *-1
	.dsb 5
Self	.long 4
Type	.word 2
	.text

	!PRO LZ
	ldy #JW_Parent
	lda [Self],y
	sta PareP
	iny
	iny
	lda [Self],y
	sta PareP+2
	ldy #JW_Flags
	lda Type
	bne gained
	lda [Self],y
	and #$ffff-(JF_Selected|JF_Focused)
	sta [Self],y
	ldy #JW_Focused
	lda [PareP],y
	cmp Self
	bne donef
	iny
	iny
	lda [PareP],y
	cmp Self+2
	bne donef
	lda #0
	sta [PareP],y
	dey
	dey
	sta [PareP],y
	bra donef
gained	pea 0
	ldy #JW_Focused+2
	lda [PareP],y
	pha
	dey
	dey
	lda [PareP],y
	pha
	ldx #MJW_Select
	jsr @VMCheck
	pla
	pla
	pla
	ldy #JW_Flags
	lda [Self],y
	ora #JF_Selected
	sta [Self],y
	ldy #JW_Focused
	lda Self
	sta [PareP],y
	iny
	iny
	lda Self+2
	sta [PareP],y
	ldy #JW_Flags
	lda [PareP],y
	and #JF_Focused
	beq donef
	pea EVS_Gained
	pei (Self+2)
	pei (Self)
	ldx #MJW_Focus
	jsr @VMC
	pla
	pla
	pla
	bra exeet
donef	pei (Self+2)
	pei (Self)
	jsr @_JWinReDraw
	pla
	pla
exeet	!POST LZ
	rtl
	
	.)
	
/* ----------------------------------------

          Redrawing and invalidating

	JWinReDraw
	JWinInvalid

------------------------------------------*/

&_JWinReDraw .(

	.abs 1
LZ	= *-1
	.dsb 5

Self	.long 4

	.text
	!PRO LZ
	ldy #JW_HideCnt
	lda [Self],y
	bne nodraw
	ldy #JW_YSize
	lda [Self],y
	pha
	ldy #JW_XSize
	lda [Self],y
	pha
	pea 0
	pea 0
	pei (Self+2)
	pei (Self)
	jsr @_JWinInvalid
	!POP 12
nodraw	!POST LZ
	rtl
	
	.)
	
&_JWinInvalid .(

	.abs 1
event	.dsb EV_SIZE
LZ	= *-1
	.dsb 5
Self	.long 4
rect	.long 8

	.text
	!PRO LZ
tryag	ldy #JW_Flags
	lda [Self],y
	and #JF_Added
	bne isadded
	ldy #JW_X
	lda [Self],y
	clc
	adc rect
	sta rect
	ldy #JW_Y
	lda [Self],y
	clc
	adc rect+2
	sta rect+2
	ldy #JW_Parent
	lda [Self],y
	tax
	ldy #JW_Parent+2
	lda [Self],y
	sta Self+2
	stx Self
	lda rect
	sec
	ldy #JW_XScrld
	sbc [Self],y
	sta rect
	lda rect+2
	sec
	ldy #JW_YScrld
	sbc [Self],y
	sta rect+2
	bra tryag
isadded	lda #WEV_Expose
	sta event+EV_Type
	ldy #JW_RegID
	lda [Self],y
	sta event+EV_Sender
	stz event+EV_DataSz
	lda #1
	sta event+EV_NumRects
	stz event+EV_SubType
	
	pea EVF_Self
	pea 0	; buf
	pea 0
	pea 0
	tdc
	clc
	adc #rect
	pha
	pea 0
	tdc
	clc
	adc #event
	pha
	pea WIN_SendEvent
	ldy #JW_Con
	lda [Self],y
	pha
	jsr @_sendCon
	!POST LZ+18
	rtl
	
	.)

&_JWinDraw	
	.(
	rtl
	.)
	
&_JWinNotice .(

	.abs 1
LZ	= *-1
	.dsb 5
Self	.long 4
SubType	.word 2
From	.word 2
DataP	.long 4
	.text
	
	!PRO LZ
	lda SubType
	cmp #EVS_Changed
	bne nochan
	lda [DataP]
	ldy #JW_X
	sta [Self],y
	ldy #2
	lda [DataP],y
	ldy #JW_Y
	sta [Self],y
	ldy #4
	lda [DataP],y
	ldy #JW_XSize
	sta [Self],y
	ldy #6
	lda [DataP],y
	ldy #JW_YSize
	sta [Self],y
	pei (Self+2)
	pei (Self)
	ldx #MJW_Resize
	jsr @VMC
	pla
	pla
	bra exeet
nochan	cmp #EVS_Shown
	bne exeet
	lda #0
	ldy #JW_HideCnt
	sta [Self],y
	ldy #JW_RegFlags
	lda [Self],y
	ora #REGF_Visible
	sta [Self],y
exeet	!POST LZ
	rtl

	.)
	
&_JWinRButton .(
	.abs 1
PareP	.long 4
LZ	= *-1
	.dsb 5

Self	.long 4
Subtype	.word 2
X	.word 2
Y	.word 2
XAbs	.word 2
YAbs	.word 2
	
	.text	
	!PRO LZ
	ldy #JW_Parent
	lda [Self],y
	sta PareP
	iny
	iny
	lda [Self],y
	sta PareP+2
	ora PareP
	beq nohigh
	pei (YAbs)
	pei (XAbs)
	pei (Y)
	pei (X)
	pei (Subtype)
	pei (PareP+2)
	pei (PareP)
	ldx #MJW_RButton
	jsr @VMC
	!POP 14
nohigh	!POST LZ
	rtl

	.)
	

&_JWinChNotice
&_JWinBound	
&_JWinMotion
&_JWinButton rtl

	!PRPCLASS JW

JWMethods	.(
	jmp (Methods,x)
Methods	.word _JWinInit	; Init
	.word _JWinKill	; Destory
	.word _JWinDraw	; Draw
	.word _JWinShow	; Show
	.word _JWinHandle	; Handle
	.word _JWinReDraw	; ReDraw
	.word _JWinFocus	; Focus change
	.word _JWinSelect	; Selected/deselected
	.word _JWinKeyD	; A key down
	.word _JWinAdd	; Add a child
	.word _JWinButton	; Button 
	.word _JWinMotion	; Motion
	.word _JWinBound	; Boundary
	.word _JWinNotice	; Notice
	.word _JWinGeom	; Geom
	.word _JWinResize	; Resize
	.word _JWinRButton	; Right button (menu)
	.word _JWinChNotice	; Child notice (moved or died etc.)
	.)

winname	.asc "/sys/win",0
nowin	.asc "No window engine!",10,0
		
&Chars	.dsb 8
	.byte $ff,$80,$80,$80,$80,$80,$80,$80
	.byte $ff,0,0,0,0,0,0,0
	.byte $ff,1,1,1,1,1,1,1
	.byte $80,$80,$80,$80,$80,$80,$80,$80
	.byte 1,1,1,1,1,1,1,1
	.byte $80,$80,$80,$80,$80,$80,$80,$ff
	.byte 0,0,0,0,0,0,0,$ff
	.byte 1,1,1,1,1,1,1,$ff
	.byte 1,1,$21,$15,$0d,$1d,1,$fe
	
&DflPen	.word $00
&DflBack .word $0f

	.include "Graphics.a65"

	.data
&JRootObj
	.dsb JW_SIZE

	
	.bss

	/* Drawing globals */

&_ClipRect	.dsb 8
&MainWin	.word 0,0
P_BufIn	.word 0
P_BufLeft	.word 0
P_Rects	.word 0,0
P_Buf	.dsb PNT_BUFSIZE
P_PenX	.word 0
P_PenY	.word 0
P_Font	.word 0
P_Mode	.word 0
P_Style	.word 0
P_Col	.word 0
P_XOffs	.word 0
P_YOffs	.word 0
P_HasClear	.word 0

P_Ev	.dsb EV_SIZE
P_BufDone	= P_Ev+EV_DataSz

WCon	.word 0
AnEvent	.dsb 2048
	
	.)
