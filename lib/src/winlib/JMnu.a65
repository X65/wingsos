
/* JMnu - Menu */

	.(

	.al
	.xl
	.pic
	
	.include	<kern.i65>
	.include	<win.i65>	
	.include	<stdio.i65>
	.include	<widget.i65>
	.include	<lcc.i65>

/*-----------------------------------------------
     Menu Methods - Inherits JWin
------------------------------------------------*/

&_JMnuInit	.(

	.abs 1
XSize	.word 2
YSize	.word 2
ItemP	.long 4
LZ	= *-1
	.dsb 5
Self	.long 4
Menu	.long 4
X	.word 2
Y	.word 2
Call	.long 4
	.text

	!PRO LZ
	!PRPOBJ JMnu
	stz XSize
	stz YSize
	lda Menu
	sta ItemP
	lda Menu+2
	sta ItemP+2
nexmenu	ldy #MENU_Name
	lda [ItemP],y
	iny
	iny
	ora [ItemP],y
	beq gotsizes
	pea 0
	pea 0
	ldy #MENU_Name+2
	lda [ItemP],y
	pha 
	dey
	dey
	lda [ItemP],y
	pha 
	jsr @_FL_extStrX
	pla
	pla
	pla
	pla
	cpx XSize
	bcc islower
	stx XSize
islower	lda YSize
	clc
	adc #8
	sta YSize
	lda #MENU_SIZE
	clc
	adc ItemP
	sta ItemP
	bcc nexmenu
	inc ItemP+2
	bra nexmenu
	
gotsizes lda XSize
	clc
	adc #26
	sta XSize
	eor #7
	inc
	and #7
	clc
	adc XSize
	sta XSize
	lda #16
	cmp YSize
	bcc hasgeom
	sta YSize
hasgeom	pea 0 ; JF_Selectable
	pea WEV_Button|WEV_Boundary|WEV_MotionBut|WEV_Notice
	pei (YSize)
	pei (XSize)
	pei (Self+2)
	pei (Self)
	jsr @_JWInit
	!POP 12
	
	pei (YSize)
	pei (XSize)
	pei (Y)
	pei (X)
	pei (Self+2)
	pei (Self)
	jsr @_JWSetBounds
	!POP 12
	
	lda Menu
	ldy #JMnu_Data
	sta [Self],y
	lda Menu+2
	iny
	iny
	sta [Self],y
	lda Call
	ldy #JMnu_Callback
	sta [Self],y
	iny
	iny
	lda Call+2
	sta [Self],y
exeet	ldx Self
	ldy Self+2
	!POST LZ
	rtl
	.)

&_JMnuDraw	.(

	.abs 1
Upto	.long 4
X8	= *
XUpto	.word 2
Y8	= *
YUpto	.word 2
NameP	.long 4
Sel	.word 2
YOffs	.word 2
PSize	.word 2
LZ	= *-1
	.dsb 5
Self	.long 4

	.text
	!PRO LZ
	pea 0
	pea 0
	jsr @_FL_extYab
	stx YOffs
	sta PSize
	pla
	pla

	ldy #JMnu_Data
	lda [Self],y
	sta Upto
	iny
	iny
	lda [Self],y
	sta Upto+2
	
	ldy #JW_YSize
	lda [Self],y
	lsr
	lsr
	lsr
	sta Y8
	pha
	ldy #JW_XSize
	lda [Self],y
	lsr
	lsr
	lsr
	sta X8
	pha
	pea ^MnuStr
	pea !MnuStr
	jsr @_GfxString
	pla
	pla
	pla
	pla
		
	lda X8
	dec
	dec
	pha
	pea ^MnuStr3
	pea !MnuStr3
	jsr @_GfxString
	pla
	pla
	pla

	lda Y8
	dec
	dec
	beq noy8
	tax
	lda X8
	dec
	dec
	pha
	pha
	phx
	pea ^MnuStr5
	pea !MnuStr5
	jsr @_GfxString
	tsc
	clc
	adc #10
	tcs
	bra donebot
	
noy8	lda X8
	dec
	dec
	pha
	pea ^MnuStr6
	pea !MnuStr6
	jsr @_GfxString
	pla
	pla
	pla

donebot	pea CHAR_End
	jsr @_GfxChar
	pla

	pea GMOD_Ora
	jsr @_GfxSetMode
	pla
	
	stz XUpto
	stz YUpto
	
nextupto	ldy #MENU_Name+2
	lda [Upto],y
	sta NameP+2
	dey
	dey
	lda [Upto],y
	sta NameP
	ora NameP+2
	bne unfin
	brl finished
	
unfin	ldy #JW_Colours
	lda [Self],y
	tax
	stz Sel
	lda Upto
	ldy #JMnu_Selected
	cmp [Self],y
	bne notsel
	lda Upto+2
	iny
	iny
	cmp [Self],y
	bne notsel
	lda #1
	sta Sel
	txa
	and #$f0
	ora #1
	bra gotcol
notsel	txa
gotcol	pha
	jsr @_GfxSetCol
	pla
		
	lda YUpto
	clc
	adc YOffs
	pha
	lda XUpto
	clc
	adc #2
	pha
	jsr @_GfxSetPen
	pla
	pla
	
	pei (NameP+2)
	pei (NameP)
	jsr @_GfxText
	pla
	pla

	ldy #MENU_Submenu
	lda [Upto],y
	iny
	iny
	ora [Upto],y
	beq nosuby
	pei (YUpto)
	ldy #JW_XSize
	lda [Self],y
	sec
	sbc #8
	pha
	jsr @_GfxSetPen
	pla
	pla
	pea ^MnuStr8
	pea !MnuStr8
	jsr @_GfxString
	pla
	pla

nosuby	lda Sel
	beq nosel
	jsr getlenx
	txa
	sta Sel
	eor #7
	inc
	and #7
	clc
	adc Sel
	sta Sel
	ldy #JW_XSize
	lda [Self],y
	sec
	sbc Sel
	bcc nosel
	beq nosel
	lsr
	lsr
	lsr
	pha
	pea 1
	pha
	pei (YUpto)
	pei (Sel)
	jsr @_GfxSetPen
	pla
	pla
	pea ^MnuStr
	pea !MnuStr
	jsr @_GfxString
	pla
	pla
	pla
	pla
	
	pea ^MnuStr7
	pea !MnuStr7
	jsr @_GfxString
	pla
	pla
	pla

	pea CHAR_End
	jsr @_GfxChar
	pla

nosel	lda YUpto
	clc
	adc PSize
	sta YUpto
	eor #7
	inc
	and #7
	adc YUpto
	sta YUpto
	lda #8
	sec
	sbc PSize
	lsr
	clc
	adc YUpto
	sta YUpto

gonext	lda #MENU_SIZE
	clc
	adc Upto
	sta Upto
	lda Upto+2
	adc #0
	sta Upto+2
	brl nextupto
	
finished	
exeet	!POST LZ
	rtl

getlenx	pea 0
	pea 0
	pei (NameP+2)
	pei (NameP)
	jsr @_FL_extStrX
	tsc
	clc
	adc #8
	tcs
	rts

MnuStr	.byte GFX_Charset
	.word !Chars
	.byte ^Chars,0
	.byte "%b%b"
	.byte "%E"
	
MnuStr5	.byte CHAR_YRep,"%b",4,CHAR_Rep,"%b",0,5
	.byte CHAR_YEnd
MnuStr6	.byte 6,CHAR_Rep,"%b",7,8
	.byte "%E"
	
MnuStr3	.byte 1,CHAR_Rep,"%b",2,3
	.byte "%E"

MnuStr7	.byte CHAR_Rep,"%b",0
	.byte "%E"
	
MnuStr8	.byte GFX_Charset
	.word !HasSubBit
	.byte ^HasSubBit,0
	.byte 1,1
	.byte 0
	.byte CHAR_End
	.byte "%E"
	
blank	.dsb 8
HasSubBit	.byte $00,$00,$60,$70,$7c,$70,$60,$00
	
	.)

_JMnuWhich	.(

	.abs 1
ItemP	.long 4
XLen	.word 2
XOffs	.word 2
YOffs	.word 2
LZ	= *-1
	.dsb 5
Self	.long 4
X	.word 2
Y	.word 2
XYPos	.long 4

	.text
	!PRO LZ
	lda X
	ldy #JW_XSize
	cmp [Self],y
	bcs outahere
	lda Y
	ldy #JW_YSize
	cmp [Self],y
	bcc okxy
outahere !jra nomoreit
okxy	stz XOffs
	stz YOffs
	ldy #JMnu_Data
	lda [Self],y
	sta ItemP
	iny
	iny
	lda [Self],y
	sta ItemP+2
gonextit	ldy #MENU_Name
	lda [ItemP],y
	iny
	iny
	ora [ItemP],y
	beq nomoreit
	lda Y
	sec
	sbc #8
	bcc gotitem
	sta Y
	lda YOffs
	clc
	adc #8
	sta YOffs
gonext	lda ItemP
	clc
	adc #MENU_SIZE
	sta ItemP
	bcc gonextit
	inc ItemP+2
	bra gonextit
gotitem	pei (XYPos+2)
	pei (XYPos)
	pei (Self+2)
	pei (Self)
	jsr @_JWAbs
	pla
	pla
	pla
	pla
	ldy #JW_XSize
	lda [Self],y
	clc
	adc [XYPos]
	sta [XYPos]
	lda YOffs
	clc
	ldy #2
	adc [XYPos],y
	sta [XYPos],y
	ldx ItemP
	ldy ItemP+2
	bra exeet	
nomoreit	ldx #0
	txy
exeet	!POST LZ
	rtl
	
	.)

&_JMnuMotion .(

	.abs 1
XGood	.word 2
YGood	.word 2
ItemP	.long 4
LZ	= *-1
	.dsb 5
Self	.long 4
Type	.word 2
X	.word 2
Y	.word 2
XAbs	.word 2
YAbs	.word 2
	.text
	
	!PRO LZ
	pea 0
	tdc
	clc
	adc #XGood
	pha
	pei (Y)
	pei (X)
	pei (Self+2)
	pei (Self)
	jsr @_JMnuWhich
	!POP 12
	stx ItemP
	sty ItemP+2
	lda ItemP
	ora ItemP+2
	beq noitem
	pei (YGood)
	pei (XGood)
	pei (ItemP+2)
	pei (ItemP)
	pei (Self+2)
	pei (Self)
	jsr @_JMnuOpSub
	!POP 12
noitem	!POST LZ
	rtl
	
	.)
	
&_JMnuButton .(
	
	.abs 1
ItemP	.long 4
MenP	.long 4
XGood	.word 2
YGood	.word 2
LZ	= *-1
	.dsb 5
Self	.long 4
SubType	.word 2
X	.word 2
Y	.word 2
XAbs	.word 2
YAbs	.word 2

	.text
	!PRO LZ
	lda X
	bmi dokill
	ldy #JW_XSize
	cmp [Self],y
	bcs dokill
	lda Y
	bmi dokill
	iny
	iny
	cmp [Self],y
	bcc nokl
dokill	!jra killmen
nokl	pea 0
	tdc
	clc
	adc #XGood
	pha
	pei (Y)
	pei (X)
	pei (Self+2)
	pei (Self)
	jsr @_JMnuWhich
	!POP 12
	stx ItemP
	sty ItemP+2
	tya
	ora ItemP
	beq noitem
	lda SubType
	cmp #EVS_But1Down
	bne notdown
	pei (YGood)
	pei (XGood)
	pei (ItemP+2)
	pei (ItemP)
	pei (Self+2)
	pei (Self)
	jsr @_JMnuOpSub
	tsc
	clc
	adc #12
	tcs
	bra noitem
notdown	ldy #MENU_Submenu+2
	lda [ItemP],y
	dey
	dey
	ora [ItemP],y
	bne noitem
	
	pei (ItemP+2)
	pei (ItemP)
	pei (Self+2)
	pei (Self)
	ldy #JMnu_Callback
	lda [Self],y
	tax
	iny
	iny
	lda [Self],y
	jsr @cb
	!POP 8
		
killmen	pei (Self+2)
	pei (Self)
	jsr @_JMnuTopM
	!POP 4
	phy
	phx
	jsr @_JMnuPopKill
	!POP 4
noitem	!POST LZ
	rtl
	
	.)


_JMnuOpSub	
	.(
	.abs 1
SubMenu	.long 4
LZ	= *-1
	.dsb 5
Self	.long 4
ItemP	.long 4
X	.word 2
Y	.word 2
	.text

	!PRO LZ
	lda ItemP
	ldy #JMnu_Selected
	cmp [Self],y
	bne difsub
	lda ItemP+2
	iny
	iny
	cmp [Self],y
	bne difsub

	ldy #JMnu_SubMenu
	lda [Self],y
	sta SubMenu
	iny
	iny
	lda [Self],y
	sta SubMenu+2
	ora SubMenu
	beq focthis
	ldy #JMnu_SubMenu
	lda [SubMenu],y
	iny
	iny
	ora [SubMenu],y
	beq nosub
	lda [SubMenu],y
	pha
	dey
	dey
	lda [SubMenu],y
	pha
	jsr @_JMnuKill
	pla
	pla
nosub	brl foclast

difsub	ldy #JMnu_SubMenu+2
	lda [Self],y
	dey
	dey
	ora [Self],y
	beq nosubm
	iny
	iny
	lda [Self],y
	pha
	dey
	dey
	lda [Self],y
	pha
	jsr @_JMnuKill
	pla
	pla
nosubm	ldy #JMnu_Selected
	lda ItemP
	sta [Self],y
	iny
	iny
	lda ItemP+2
	sta [Self],y
	pei (Self+2)
	pei (Self)
	jsr @_JWReDraw
	pla
	pla
	lda #0
	ldy #JMnu_SubMenu
	sta [Self],y
	iny
	iny
	sta [Self],y
	
	ldy #MENU_Submenu
	lda [ItemP],y
	iny
	iny
	ora [ItemP],y
	bne issub
focthis	lda Self+2
	sta SubMenu+2
	lda Self
	sta SubMenu
	bra foclast
issub	ldy #JMnu_Callback+2
	lda [Self],y
	pha
	dey
	dey
	lda [Self],y
	pha
	pei (Y)
	pei (X)
	ldy #MENU_Submenu+2
	lda [ItemP],y
	pha
	dey
	dey
	lda [ItemP],y
	pha
	pea 0
	pea 0
	jsr @_JMnuInit
	!POP 16
	stx SubMenu
	sty SubMenu+2
	lda Self
	ldy #JTop_PrevTop
	sta [SubMenu],y
	iny
	iny
	lda Self+2
	sta [SubMenu],y
	ldy #JMnu_SubMenu
	lda SubMenu
	sta [Self],y
	iny
	iny
	lda SubMenu+2
	sta [Self],y
	ldy #JMnu_Pare
	lda Self
	sta [SubMenu],y
	iny
	iny
	lda Self+2
	sta [SubMenu],y
	pei (SubMenu+2)
	pei (SubMenu)
	jsr @_JWShow
	pla
	pla
	bra exeet
foclast	
exeet	!POST LZ
	rtl
	
	.)

&_JMnuNotice .(

	.abs 1
LZ	= *-1
	.dsb 5
Self	.long 4
SubType	.word 2
From	.word 2
DataP	.long 4

	.text
	!PRO LZ
/*	pei (Self+2)
	pei (Self)
	!POUT "MnuNotice %lx\n"
	!POP 4*/
	bra skipfir
goag	ldy #JMnu_Pare
	lda [Self],y
	tax
	iny
	iny
	lda [Self],y
	sta Self+2
	stx Self
	ldy #JW_RegID
	lda [Self],y
	cmp From
	beq nomen
skipfir	ldy #JMnu_Pare
	lda [Self],y
	iny
	iny
	ora [Self],y
	bne goag
	pei (Self+2)
	pei (Self)
	jsr @_JMnuPopKill
	pla
	pla	
nomen	!POST LZ
	rtl
	
	.)

_JMnuTopM	.(

	.abs 1
LZ	= *-1
	.dsb 5
Self	.long 4
	.text

	!PRO LZ
goag	ldy #JMnu_Pare
	lda [Self],y
	iny
	iny
	ora [Self],y
	beq gotop
	lda [Self],y
	tax
	dey
	dey
	lda [Self],y
	sta Self
	stx Self+2
	bra goag
gotop	ldx Self
	ldy Self+2
	!POST LZ
	rtl
	
	.)

_JMnuPopKill .(

	.abs 1
LZ	= *-1
	.dsb 5

Self	.long 4
	.text

	!PRO LZ
	pei (Self+2)
	pei (Self)
	jsr @_JMnuTopM
	pla
	pla
	stx Self
	sty Self+2
	
	pei (Self+2)
	pei (Self)
	jsr @_JMnuKill
	!POST LZ+4
	rtl
	
	.)
	
&_JMnuKill	.(

	.abs 1
MenP	.long 4
LZ	= *-1
	.dsb 5
Self	.long 4
	.text

	!PRO LZ
	ldy #JMnu_Pare
	lda [Self],y
	sta MenP
	iny
	iny
	lda [Self],y
	sta MenP+2
	ora MenP
	beq nopare
	lda #0
	ldy #JMnu_SubMenu
	sta [MenP],y
	iny
	iny
	sta [MenP],y
	ldy #JMnu_Selected
	sta [MenP],y
	iny
	iny
	sta [MenP],y
	pei (MenP+2)
	pei (MenP)
	jsr @_JWReDraw
	pla
	pla
nopare	ldy #JMnu_SubMenu+2
	lda [Self],y
	dey
	dey
	ora [Self],y
	beq nosubm
	ldy #JMnu_SubMenu+2
	lda [Self],y
	pha
	dey
	dey
	lda [Self],y
	pha
	jsr @_JMnuKill
	pla
	pla
nosubm	pei (Self+2)
	pei (Self)
	jsr @_JWKill
	pla
	pla
	!POST LZ
	rtl
	
	.)
	
&_JMnuShow
	.(
	.abs 1
LZ	= *-1
	.dsb 5
Self	.long 4
	.text

	!PRO LZ
	pea 0
	pei (Self+2)
	pei (Self)
	jsr @_JWShow
	ldy #JTop_PrevTop
	lda [Self],y
	iny
	iny
	ora [Self],y
	bne notfir
	jsr @_JWMouFoc
	jsr @_JWModal
notfir	jsr @_JWReqFocus
	jsr @_JWToFront
	!POST LZ+6
	rtl
	
	.)	
	
	!PRPCLASS JMnu

JMnuMethods .(
	jmp (Methods,x)
Methods	.long _JWInit	; Init
	.long _JMnuKill	; Destory
	.long _JMnuDraw	; Draw
	.long _JMnuShow	; Show
	.long _JWHide	; Hide
	.long _JWHandle	; Handle
	.long _JWNotify	; Notify
	.long _JWGetHints ; Notify

	.long _JWKeyD	; A key down
	.long _JMnuButton ; Button 
	.long _JMnuButton ; Right button (menu)
	.long _JMnuMotion ; Motion
	.long _JWBound ; Boundary
	.long _JMnuNotice ; Notice
	
	.)

	.)
