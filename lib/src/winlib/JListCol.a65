
	.(

	.al
	.xl
	.pic
	
	.include	<kern.i65>
	.include	<win.i65>	
	.include	<stdio.i65>
	.include	<widget.i65>
	.include	<dirent.i65>
	.include	<65816.i65>
	.include	<lcc.i65>

/*-----------------------------------------------
     Tree Column View widget
------------------------------------------------*/

&_JListColInit	
	.(

	.abs 1
LZ	= *-1
	.dsb 5
Self	.long 4
Tree	.long 4
Title	.long 4
Width	.word 2
Offset	.long 4
Type	.word 2

	.text
	
	!PRO LZ
	!PRPOBJ JListCol
	pea JF_Selectable
	pea WEV_Button+WEV_Boundary
	pea 80
	pea 80
	pei (Self+2)
	pei (Self)
	jsr @_JWInit
	!POP 12
	pei (Title+2)
	pei (Title)
	!POUT "Title of col '%s'\n"
	!POP 4
	lda #32767
	ldy #JW_MaxYS
	sta [Self],y
	ldy #JCol_Offset
	lda Offset
	sta [Self],y
	iny
	iny
	lda Offset+2
	sta [Self],y
	ldy #JCol_List
	lda Tree
	sta [Self],y
	iny
	iny
	lda Tree+2
	sta [Self],y
		
	ldy #JCol_Type
	lda Type
	sta [Self],y
	ldy #JW_Colours
	lda [Tree],y
	sta [Self],y
exeet	ldx Self
	ldy Self+2
	!POST LZ
	rtl
	.)

&_JListColButton
	.(

	.abs 1
Iter	.dsb TreeIter_SIZE
TreP	.long 4
LZ	= *-1
	.dsb 5
Self	.long 4
Type	.word 2
X	.word 2
Y	.word 2

	.text
	!PRO LZ
	lda Type
	and #EVS_But1Down
	!jne exeet
	ldy #JCol_List
	lda [Self],y
	sta TreP
	iny
	iny
	lda [Self],y
	sta TreP+2
	
	pea 0
	tdc
	inc
	pha
	pei (TreP+2)
	pei (TreP)
	jsr @_JListGetIter
	!POP 8
	
	lda Y
	clc
	ldy #JList_YScroll
	adc [TreP],y
	sta Y
	
nxone	pea 0
	tdc
	inc
	pha
	pei (TreP+2)
	pei (TreP)
	jsr @_JListNextItem
	!POP 8	
	txa
	!jne exeet
	
	lda Y
	sec
	sbc #8
	sta Y
	!jpl nxone
	
	pei (Iter+TreeIter_ItemP+2)
	pei (Iter+TreeIter_ItemP)
	pei (TreP+2)
	pei (TreP)
	jsr @_JListItemClicked
	!POP 8
	
exeet	!POST LZ
	rtl
	
	.)
		
IcoStr	.byte GFX_Bitmap,"%D"
	.byte 2,1
	.byte BITT_Seper,"%D"
	.byte "%E"
	
ChString
	.byte GFX_Charset
	.byte "%D"
	.byte 1,1
	.byte "%b"
	.byte CHAR_End
	.byte "%E"
	
ChSet	.dsb 8
	.byte 0,0,$7c,$6c,$47,$6c,$7c,0
	.byte 0,0,$7c,$7c,$47,$7c,$7c,0


	!PRPCLASS JListCol
			
JListColMethods	.(
	jmp (Methods,x)
Methods	.long _JWInit	; Init
	.long _JWKill	; Destory
	.long _JListColDraw	; Draw
	.long _JWShow	; Show
	.long _JWHide	; Hide
	.long _JWHandle	; Handle
	.long _JWNotify ; Notify
	.long _JWGetHints ; Notify

	.long _JWKeyD	; A key down
	.long _JListColButton ; Button 
	.long _JWRButton ; Right button (menu)
	.long _JWMotion	; Motion
	.long _JWBound ; Boundary
	.long _JWNotice	; Notice
	
	.)

	.)
	
