
/* JChk - Checkbutton */

	.(

	.al
	.xl
	.pic
	
	.include	<kern.i65>
	.include	<win.i65>	
	.include	<stdio.i65>
	.include	<widget.i65>
	.include	<lcc.i65>

/*-----------------------------------------------
     Checkbox Methods - Inherits JWin
------------------------------------------------*/

&_JChkInit	.(

	.abs 1
LZ	= *-1
	.dsb 5

Self	.long 4
Label	.long 4

	.text
	!PRO LZ
	!PRPOBJ JChk
	pea 0
	pea 0
	pei (Label+2)
	pei (Label)
	jsr @_FL_extStrX
	!POP 8
	pea JF_Selectable
	pea WEV_Button|WEV_Boundary
	pea 8
	txa
	clc
	adc #8
	and #$ff-7
	clc
	adc #8
	pha
	pei (Self+2)
	pei (Self)
	jsr @_JWInit
	!POP 12
	lda Label
	ldy #JChk_Label
	sta [Self],y
	iny
	iny
	lda Label+2
	sta [Self],y
exeet	ldx Self
	ldy Self+2
	!POST LZ
	rtl
	.)

&_JChkDraw	.(
	.abs 1
LZ	= *-1
	.dsb 5

Self	.long 4
	
	.text
	!PRO LZ
	pea 0
	pea 0
	jsr @_GfxSetPen
	pla
	pla
	
	ldy #JChk_Status
	lda [Self],y
	and #JCF_Ticked
	beq notick
	lda #1
notick	pha
	lda [Self],y
	and #JCF_Entered+JCF_Butdown
	cmp #JCF_Entered+JCF_Butdown
	beq isdown
	pea $01
	bra gotc
isdown	pea $0c
gotc	pea ^ChkBmp
	pea !ChkBmp
	pea ^ChkStr
	pea !ChkStr
	jsr @_GfxString
	tsc
	clc
	adc #12
	tcs
	
	pea 8
	pea 10
	jsr @_GfxSetPen
	pla
	pla
	
	
	lda @DflPen
	ora @DflBack
	pha
	jsr @_GfxSetCol
	pla

	pea 0
	jsr @_GfxSetFont
	pla
	
	ldy #JChk_Label+2
	lda [Self],y
	pha
	dey
	dey
	lda [Self],y
	pha
	jsr @_GfxText
	pla
	pla
exeet	!POST LZ
	rtl

ChkStr	.byte GFX_Charset,"%D"
	.byte 1,1,CHAR_Col,"%b%b"
	.byte CHAR_End
	.byte "%E"
	
	.)

&_JChkButton .(
	.abs 1
LZ	= *-1
	.dsb 5
Self	.long 4
SubType	.word 2

	.text
	!PRO LZ
	lda SubType
	cmp #EVS_But1Down
	beq isdown
	ldy #JChk_Status
	lda [Self],y
	and #JCF_Entered
	bne oktick
	lda [Self],y
	bra clrbut
oktick	lda #JCF_Ticked
	eor [Self],y
clrbut	and #-1-JCF_Butdown
	sta [Self],y
	bra redr
isdown	lda #JCF_Butdown
	ldy #JChk_Status
	ora [Self],y
	sta [Self],y
	pei (Self+2)
	pei (Self)
	jsr @_JWMouFoc
	pla
	pla
	bra redr
redr	pei (Self+2)
	pei (Self)
	jsr @_JWReDraw
	pla
	pla
exeet	!POST LZ
	rtl
	
	.)

&_JChkBound .(

	.abs 1	
LZ	= *-1
	.dsb 5
Self	.long 4
SubType	.word 2

	.text
	!PRO LZ
	lda SubType
	cmp #EVS_Entered
	bne left
	ldy #JChk_Status
	lda #JCF_Entered
	ora [Self],y
	sta [Self],y
	bra redr
left	ldy #JChk_Status
	lda #-1-JCF_Entered
	and [Self],y
	sta [Self],y
redr	pei (Self+2)
	pei (Self)
	jsr @_JWReDraw
	pla
	pla
exeet	!POST LZ
	rtl
	
	.)
	
ChkBmp	.byte $ff,$81,$81,$81,$81,$81,$81,$ff
	.byte $ff,$81,$83,$85,$a9,$91,$81,$ff

        !PRPCLASS JChk
	
JChkMethods .(
	jmp (Methods,x)
Methods	.long _JWInit	; Init
	.long _JWKill	; Destory
	.long _JChkDraw	; Draw
	.long _JWShow	; Show
	.long _JWHide	; Hide
	.long _JWHandle	; Handle
	.long _JWNotify ; Notify
	.long _JWGetHints ; Notify

	.long _JWKeyD	; A key down
	.long _JChkButton ; Button 
	.long _JWRButton ; Right button (menu)
	.long _JWMotion	; Motion
	.long _JChkBound ; Boundary
	.long _JWNotice	; Notice
	.)

	.)
