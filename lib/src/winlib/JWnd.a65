	.(

	.al
	.xl
	.pic
	
	.include	<kern.i65>
	.include	<win.i65>	
	.include	<stdio.i65>
	.include	<widget.i65>
	.include	<font.i65>
	.include	<lcc.i65>

/*-----------------------------------------------
     Window Widget Methods - Inherits JWin
------------------------------------------------*/


&_JWndInit	.(

	.abs 1
RegProp	.dsb REGP_SIZE
LZ	= *-1
	.dsb 5

Self	.long 4
Parent	.long 4
Flags	.word 2
Label	.long 4
CFlags	.word 2

	.text

	!PRO LZ
	!PRPOBJ JWnd
	lda #JF_Selectable+JF_Manage
	ora Flags
	pha
	pea 0
	pei (Parent+2)
	pei (Parent)
	pea 140
	pea 240
	pea 8
	pea 8
	pei (Self+2)
	pei (Self)
	jsr @_JWinInit
	!POP 20
	lda #24
	ldy #JW_MinXS
	sta [Self],y
	ldy #JW_MinYS
	sta [Self],y
	pei (Label+2)
	pei (Label)
	jsr @_strdup
	pla
	pla
	tya
	ldy #JWnd_Label+2
	sta [Self],y
	txa
	dey
	dey
	sta [Self],y
			
	lda CFlags
	ldy #JWnd_Flags
	sta [Self],y
	sta RegProp+REGP_Flags
	
	ldy #JWnd_Label
	lda [Self],y
	sta RegProp+REGP_Name
	ldy #JWnd_Label+2
	lda [Self],y
	sta RegProp+REGP_Name+2
	
	lda #24
	sta RegProp+REGP_MinX
	sta RegProp+REGP_MinY
	lda #32767
	sta RegProp+REGP_MaxX
	sta RegProp+REGP_MaxY
	
	stz RegProp+REGP_Icon
	stz RegProp+REGP_Icon+2
	
	pea 0
	tdc
	clc
	adc #RegProp
	pha
	ldy #JW_RegID
	lda [Self],y
	pha
	pea WIN_EditProp
	ldy #JW_Con
	lda [Self],y
	pha
	jsr @_sendCon
	!POP 10
	
exeet	ldx Self
	ldy Self+2
	!POST LZ
	rtl

	.)

&_JWndDefault .(

	rtl
	
	.)
	
&_JWndRButton .(

	.abs 1
LZ	= *-1
	.dsb 5

Self	.long 4
Type	.word 2
X	.word 2
Y	.word 2
XAbs	.word 2
YAbs	.word 2

	.text

	!PRO LZ
	lda Type
	cmp #EVS_But2Up
	bne nocl
	pei (YAbs)
	pei (XAbs)
	pei (Y)
	pei (X)
	pei (Type)
	pei (Self+2)
	pei (Self)
	ldy #JWnd_RightClick
	lda [Self],y
	tax
	iny
	iny
	lda [Self],y
	jsr @cb
	!POP 14
nocl	!POST LZ
	rtl
	
	.)
	
&_JWndDraw	.(
	
	jmp @_GfxClear
	
	.)
	
	!PRPCLASS JWnd
	
JWndMethods .(
	jmp (Methods,x)
Methods	.word _JWndInit
	.word _JWinKill
	.word _JWndDraw
	.word _JWinShow
	.word _JWinHandle
	.word _JWinReDraw
	.word _JWinFocus
	.word _JWinSelect
	.word _JWinKeyD
	.word _JWinAdd
	.word _JWinButton
	.word _JWinMotion
	.word _JWinBound
	.word _JWinNotice
	.word _JWinGeom
	.word _JWinResize
	.word _JWndRButton
	.word _JWinChNotice
	.)

	.)

