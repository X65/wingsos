	.(

	.al
	.xl
	.pic
	
	.include	<kern.i65>
	.include	<win.i65>	
	.include	<stdio.i65>
	.include	<widget.i65>
	.include	<lcc.i65>


/*-----------------------------------------------
     Icon Button Widget Methods - Inherits JBut
------------------------------------------------*/

&_JIbtInit	.(
	.abs 1
mreg	.long 4
mreg2	.long 4
LZ	= *-1
	.dsb 5
Self	.long 4
XSize	.word 2
YSize	.word 2
IconUp	.long 4
IconDown .long 4

	.text
	!PRO LZ
	!PRPOBJ JIbt
	pea JF_Selectable
	pea WEV_Button+WEV_Boundary
	pei (YSize)
	pei (XSize)
	pei (Self+2)
	pei (Self)
	jsr @_JWInit
	!POP 12
	
	lda IconUp
	ldy #JIbt_IconUp
	sta [Self],y
	lda IconUp+2
	iny
	iny
	sta [Self],y
	lda IconDown
	ldy #JIbt_IconDown
	sta [Self],y
	lda IconDown+2
	iny
	iny
	sta [Self],y
	lda XSize
	lsr
	lsr
	lsr
	sta mreg2
	lda YSize
	jsr @__muli16
	ldy #JIbt_BitSize
	sta [Self],y
exeet	ldx Self
	ldy Self+2
	!POST LZ
	rtl
	.)

&_JIbtDraw	.(

Bitmap	= 1
LZ	= 4

Self	= LZ + 6
	

	phd
	tsc
	sec
	sbc #LZ
	tcs
	tcd
	ldy #JBut_Flags
	lda [Self],y
	and #JBF_Butdown+JBF_Entered
	cmp #JBF_Butdown+JBF_Entered
	beq isdown
isup2	ldy #JIbt_IconUp
	bra isup
isdown	ldy #JIbt_IconDown
	lda [Self],y
	ldy #JIbt_IconDown+2
	ora [Self],y
	bne hasdown
	pea GMOD_Inverted
	jsr @_GfxSetMode
	pla
	bra isup2
hasdown	ldy #JIbt_IconDown
isup	lda [Self],y
	sta Bitmap
	iny
	iny
	lda [Self],y
	sta Bitmap+2
	
	lda Bitmap
	clc
	ldy #JIbt_BitSize
	adc [Self],y
	tax
	lda Bitmap+2
	adc #0
	pha
	phx
	ldy #JW_YSize
	lda [Self],y
	lsr
	lsr
	lsr
	pha
	ldy #JW_XSize
	lda [Self],y
	lsr
	lsr
	lsr
	pha
	pei (Bitmap+2)
	pei (Bitmap)
	pea ^IbtStr
	pea !IbtStr
	jsr @_GfxString
	tsc
	clc
	adc #16+LZ
	tcs
	pld
	rtl
	
IbtStr	.byte GFX_Bitmap,"%D"
	.byte "%b%b"
	.byte BITT_Seper,"%D"
	.byte "%E"
	
	.)

       !PRPCLASS JIbt
       
JIbtMethods .(
	jmp (Methods,x)
Methods	.long _JWInit	; Init
	.long _JWKill	; Destory
	.long _JIbtDraw	; Draw
	.long _JWShow	; Show
	.long _JWHide	; Hide
	.long _JWHandle	; Handle
	.long _JWNotify	; Notify
	.long _JWGetHints ; Notify

	.long _JWKeyD	; A key down
	.long _JButButton ; Button 
	.long _JWRButton ; Right button (menu)
	.long _JWMotion	; Motion
	.long _JButBound ; Boundary
	.long _JWNotice	; Notice
	
	.)

	.)
