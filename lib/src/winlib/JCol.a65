
	.(

	.al
	.xl
	.pic
	
	.include	<kern.i65>
	.include	<win.i65>	
	.include	<stdio.i65>
	.include	<widget.i65>
	.include	<dirent.i65>
	.include	<65816.i65>
	.include	<lcc.i65>

/*-----------------------------------------------
     Column widget
------------------------------------------------*/

&_JColInit	.(

	.abs 1
TBut	.long 4
ColV	.long 4
LZ	= *-1
	.dsb 5
Self	.long 4
Tree	.long 4
Title	.long 4
Width	.word 2
Offset	.long 4
Type	.word 2

	.text
	
	!PRO LZ
	!PRPOBJ JCol
	pea JF_Selectable
	pea WEV_Button
	pea 80
	pei (Width)
	pei (Self+2)
	pei (Self)
	jsr @_JWInit
	!POP 12
	ldy #JW_Colours
	lda [Tree],y
	sta [Self],y
	
	lda #32767
	ldy #JW_MaxYS
	sta [Self],y
	ldy #JCol_Offset
	lda Offset
	sta [Self],y
	iny
	iny
	lda Offset+2
	sta [Self],y
	
	ldy #JCnt_Orient
	lda #2
	sta [Self],y
	
	pei (Title+2)
	pei (Title)
	pea 0
	pea 0
	jsr @_JButInit
	!POP 8
	stx TBut
	sty TBut+2
	
	ldy #JW_PrefYS
	lda [Self],y
	pha
	pea 32767
	pei (TBut+2)
	pei (TBut)
	jsr @_JWSetMax
	!POP 8
	
	pei (Type)
	pei (Offset+2)
	pei (Offset)
	pei (Tree+2)
	pei (Tree)
	pea 0
	pea 0
	jsr @_JColVInit
	!POP 14
	stx ColV
	sty ColV+2
	
	pea 32767
	pea 32767
	pei (ColV+2)
	pei (ColV)
	jsr @_JWSetMax
	!POP 8
		
	ldy #JW_Colours
	lda [Self],y
	sta [ColV],y
	
	pei (TBut+2)
	pei (TBut)
	pei (Self+2)
	pei (Self)
	jsr @_JCntAdd
	!POP 8
	
	pei (ColV+2)
	pei (ColV)
	pei (Self+2)
	pei (Self)
	jsr @_JCntAdd
	!POP 8
	
	ldy #JCol_Title
	lda TBut
	sta [Self],y
	iny
	iny
	lda TBut+2
	sta [Self],y
	
	ldy #JCol_ColV
	lda ColV
	sta [Self],y
	iny
	iny
	lda ColV+2
	sta [Self],y
	
	ldy #JCol_Type
	lda Type
	sta [Self],y
exeet	ldx Self
	ldy Self+2
	!POST LZ
	rtl
	.)


&_JColReDraw	
	.(
	.abs 1
LZ 	= *-1
	.dsb 5
Self	.long 4
	.text
	!PRO LZ
	ldy #JCol_ColV+2
	lda [Self],y
	pha
	dey
	dey
	lda [Self],y
	pha
	jsr @_JWReDraw
	!POST LZ+4
	rtl
	.)
	
	!PRPCLASS JCol
			
JColMethods	.(
	jmp (Methods,x)
Methods	.long _JCntInit	; Init
	.long _JCntKill	; Destory
	.long _JCntDraw	; Draw
	.long _JCntShow	; Show
	.long _JCntHide	; Hide
	.long _JCntHandle	; Handle
	.long _JCntNotify	; Notify
	.long _JWGetHints ; Notify

	.long _JCntKeyD	; A key down
	.long _JWButton	; Button 
	.long _JWRButton ; Right button (menu)
	.long _JWMotion	; Motion
	.long _JWBound	; Boundary
	.long _JWNotice	; Notice

	.long _JCntAdd	; Add
	.long _JCntRemove ; Remove
	.long _JCntLayout
	
	.)

	.)
	
