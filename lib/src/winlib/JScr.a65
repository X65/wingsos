	.(

	.al
	.xl
	.pic
	
	.include	<kern.i65>
	.include	<win.i65>	
	.include	<stdio.i65>
	.include	<widget.i65>
	.include	<lcc.i65>


/*-----------------------------------------------
           Scroll area Widget - Jwin
------------------------------------------------*/

&_JScrInit	.(

	.abs 1
VBarP	.long 4
LZ	= *-1
	.dsb 5
Self	.long 4
View	.long 4
Flags	.word 2

	.text

	!PRO LZ
	!PRPOBJ JScr
	pea JF_Selectable
	pea WEV_Button+WEV_Boundary
	pea 128
	pea 128
	pei (Self+2)
	pei (Self)
	jsr @_JWInit
	!POP 12
	lda #32767
	ldy #JW_MaxXS
	sta [Self],y
	ldy #JW_MaxYS
	sta [Self],y
	lda #0
	ldy #JW_MinXS
	sta [Self],y
	ldy #JW_MinYS
	sta [Self],y
	ldy #JScr_VBar
	lda Flags
	and #JScrF_VNotEnd
	bne noend
	lda #SBAR_GoEnd
noend	jsr makebar
	ldy #JScr_HBar
	lda Flags
	and #JScrF_HNotEnd
	bne noend2
	lda #SBAR_GoEnd
noend2	jsr makebar
	lda Flags
	ora #JScrF_HVis|JScrF_VVis
	ldy #JScr_SFlags
	sta [Self],y
	pea 0
	pea 0
	jsr @_JFilInit
	!POP 4
	stx VBarP
	sty VBarP+2
	tya
	ldy #JScr_Corner+2
	sta [Self],y
	dey
	dey
	txa
	sta [Self],y
	ldy #JScr_View
	lda View
	sta [Self],y
	iny
	iny
	lda View+2
	sta [Self],y
	
	pei (Self+2)
	pei (Self)
	pei (View+2)
	pei (View)
	jsr @_JViewSetScr
	!POP 8
	
	pei (View+2)
	pei (View)
	pei (Self+2)
	pei (Self)
	jsr @_JCntAdd
	pei (VBarP+2)
	pei (VBarP)
	pei (Self+2)
	pei (Self)
	jsr @_JCntAdd
	!POP 16
		
exeet	ldx Self
	ldy Self+2
	!POST LZ
	rtl

makebar	phy
	cpy #JScr_VBar
	beq isvb
	ora #SBAR_Horiz
isvb	pha
	pea 0
	pea 0
	jsr @_JBarInit
	!POP 6
	stx VBarP
	sty VBarP+2
	tya
	ply
	iny
	iny
	sta [Self],y
	txa
	dey
	dey
	sta [Self],y
		
	; norty!
	pei (Self+2)
	pei (Self)
	pei (VBarP+2)
	pei (VBarP)
	jsr @_JWSetData
	pei (Self+2)
	pei (Self)
	jsr @_JCntAdd
	!POP 12
	
	ldy #JBar_Changed
	lda #!_JScrMove
	sta [VBarP],y
	iny
	iny
	lda #^_JScrMove
	sta [VBarP],y
	rts

	.)

_JScrMove .(

	.abs 1
VBar	= *
HBar	.long 4
LZ	= *-1
	.dsb 5
Self	.long 4
Type	.word 2

	.text
	!PRO LZ
	pei (Self+2)
	pei (Self)
	jsr @_JWGetData
	pla
	pla
	stx Self
	sty Self+2
	ldy #JScr_VBar
	lda [Self],y
	sta VBar
	iny
	iny
	lda [Self],y
	sta VBar+2
	ldy #JBar_Value+2
	lda [VBar],y
	pha
	dey
	dey
	lda [VBar],y
	pha
	ldy #JScr_HBar
	lda [Self],y
	sta HBar
	iny
	iny
	lda [Self],y
	sta HBar+2
	ldy #JBar_Value+2
	lda [HBar],y
	pha
	dey
	dey
	lda [HBar],y
	pha
	ldy #JScr_View+2
	lda [Self],y
	pha
	dey
	dey
	lda [Self],y
	pha
;	!POUT "Scrolled %lx %ld %ld\n"
	ldx #MJView_Scrolled
	jsr @VMC
	!POST LZ+12
	rtl
	
	.)

	
/*
&_JScrChNotice .(

	.abs 1
MaxX	.word 2
MaxY	.word 2
LZ	= *-1
	.dsb 5
Self	.long 4
WinP	.long 4
Type	.word 2
	
	.text
	
	!PRO LZ
	ldy #JScr_BVis
	lda [Self],y
	cmp #4
	!jeq exeet
	stz MaxX
	stz MaxY
	ldy #JW_BackCh
	lda [Self],y
	sta WinP
	iny
	iny
	lda [Self],y
	sta WinP+2
nxsib	lda WinP
	ora WinP+2
	beq donescr
	ldy #JW_Flags
	lda [WinP],y
	and #JF_Meta
	bne nomove
	ldy #JW_X
	lda [WinP],y
	clc
	ldy #JW_XSize
	adc [WinP],y
	cmp MaxX
	bcc lowx
	sta MaxX
lowx	ldy #JW_Y
	lda [WinP],y
	clc
	ldy #JW_YSize
	adc [WinP],y
	cmp MaxY
	bcc nomove
	sta MaxY
nomove	ldy #JW_Next
	lda [WinP],y
	tax
	iny
	iny
	lda [WinP],y
	sta WinP+2
	stx WinP
	bra nxsib
donescr	lda MaxX
	ldy #JScr_MaxX
	sta [Self],y
	lda MaxY
	ldy #JScr_MaxY
	sta [Self],y
	pei (Self+2)
	pei (Self)
	jsr @_JScrPrpBars
	pla
	pla
exeet	!POST LZ
	rtl

	.)
*/
		
&_JScrSync
	.(

	.abs 1
VBar	.long 4
HBar	.long 4
View	.long 4
prev	.word 2
NFlags	.word 2
LZ	= *-1
	.dsb 5
Self	.long 4

	.text

	!PRO LZ
	ldy #JScr_HBar+2
	lda [Self],y
	sta HBar+2
	dey
	dey
	lda [Self],y
	sta HBar
	ldy #JScr_VBar+2
	lda [Self],y
	sta VBar+2
	dey
	dey
	lda [Self],y
	sta VBar
	ldy #JScr_View
	lda [Self],y
	sta View
	iny
	iny
	lda [Self],y
	sta View+2
	ldy #JScr_SFlags
	lda [Self],y
	sta prev
	
	stz NFlags
	ldy #JView_VisX
	lda [View],y
	pha
	ldy #JView_MaxX+2
	lda [View],y
	pha
	dey
	dey
	lda [View],y
	pha
	pei (HBar+2)
	pei (HBar)
;	!POUT "%lx MaxX %ld %d\n"
	jsr @_JBarSetParams
	!POP 10
	lda prev
	and #JScrF_HAlways
	bne hvis
	txa
	beq nvx
	lda prev
	and #JScrF_HNever
	bne nvx
hvis	lda #JScrF_HVis
	tsb NFlags

nvx	ldy #JView_VisY
	lda [View],y
	pha
	ldy #JView_MaxY+2
	lda [View],y
	pha
	dey
	dey
	lda [View],y
	pha
	pei (VBar+2)
	pei (VBar)
;	!POUT "%lx MaxY %ld %d\n"
	jsr @_JBarSetParams
	!POP 10
	lda prev
	and #JScrF_VAlways
	bne vvis
	txa
	beq nvy
	lda prev
	and #JScrF_VNever
	bne nvy
vvis	lda #JScrF_VVis
	tsb NFlags
nvy	ldx #0
	lda prev
	and #JScrF_HVis|JScrF_VVis
	cmp NFlags
	!jeq exeet
	lda NFlags
	eor prev
	sta prev
	and #JScrF_HVis
	beq sameh
	pei (HBar+2)
	pei (HBar)
	lda NFlags
	and #JScrF_HVis
	beq noh
	jsr @_JWShow
	bra donh
noh	jsr @_JWHide
donh	!POP 4
sameh	lda prev
	and #JScrF_VVis
	beq samev
	pei (VBar+2)
	pei (VBar)
	lda NFlags
	and #JScrF_VVis
	beq nov
	jsr @_JWShow
	bra donv
nov	jsr @_JWHide
donv	!POP 4
samev	ldy #JScr_Corner
	lda [Self],y
	sta VBar
	iny
	iny
	lda [Self],y
	sta VBar+2
	pei (VBar+2)
	pei (VBar)
	ldy #JW_HideCnt
	lda [VBar],y
	cmp [Self],y
	bne ishid
	jsr @_JWHide
	bra nohid
ishid	lda NFlags
	cmp #JScrF_HVis|JScrF_VVis
	bne nohid
	jsr @_JWShow
nohid	!POP 4
	ldy #JScr_SFlags
	lda [Self],y
	and #$ffff-JScrF_HVis|JScrF_VVis
	ora NFlags
	sta [Self],y
	pei (Self+2)
	pei (Self)
	jsr @_JScrLayout
	!POP 4
	ldx #1
exeet	!POST LZ
	rtl

	.)
	
&_JScrLayout
	.(

	.abs 1
VBar	.long 4
HBar	.long 4
XSize	.word 2
YSize	.word 2
Flags	.word 2
LZ	= *-1
	.dsb 5
Self	.long 4

	.text

	!PRO LZ
	ldy #JScr_HBar+2
	lda [Self],y
	sta HBar+2
	dey
	dey
	lda [Self],y
	sta HBar
	ldy #JScr_VBar+2
	lda [Self],y
	sta VBar+2
	dey
	dey
	lda [Self],y
	sta VBar
	ldy #JScr_SFlags
	lda [Self],y
	and #JScrF_HVis|JScrF_VVis
	sta Flags
	ldy #JW_XSize
	lda [Self],y
	sta XSize
	ldy #JW_YSize
	lda [Self],y
	sta YSize
	lda Flags
	!jeq gotvsz
	and #JScrF_HVis
	beq noh
	lda YSize
	sec
	ldy #JW_PrefYS
	sbc [HBar],y
	sta YSize
noh	lda Flags
	and #JScrF_VVis
	beq nov
	lda XSize
	sec
	ldy #JW_PrefXS
	sbc [VBar],y
	sta XSize
	pei (YSize)
	ldy #JW_PrefXS
	lda [VBar],y
	pha
	pea 0
	pei (XSize)
	pei (VBar+2)
	pei (VBar)
	jsr @_JWSetBounds
	!POP 12
nov	lda Flags
	and #JScrF_HVis
	beq noh2
	ldy #JW_PrefYS
	lda [HBar],y
	pha
	pei (XSize)
	pei (YSize)
	pea 0
	pei (HBar+2)
	pei (HBar)
	jsr @_JWSetBounds
	!POP 12
noh2	lda Flags
	cmp #JScrF_HVis|JScrF_VVis
	bne gotvsz
	ldy #JW_PrefYS
	lda [HBar],y
	pha
	ldy #JW_PrefXS
	lda [VBar],y
	pha
	pei (YSize)
	pei (XSize)
	ldy #JScr_Corner+2
	lda [Self],y
	pha
	dey
	dey
	lda [Self],y
	pha
	jsr @_JWSetBounds
	!POP 12
gotvsz	pei (YSize)
	pei (XSize)
	pea 0
	pea 0
	ldy #JScr_View+2
	lda [Self],y
	pha
	dey
	dey
	lda [Self],y
	pha
	jsr @_JWSetBounds
	!POP 12
exeet	!POST LZ
	rtl

	.)
	
/*
&_JScrScroll .(

	.abs 1
VBar	.long 4
HBar	.long 4
WinP	.long 4
LZ	= *-1
	.dsb 5
Self	.long 4

	.text
	
	!PRO LZ
	ldy #JScr_HBar+2
	lda [Self],y
	sta HBar+2
	dey
	dey
	lda [Self],y
	sta HBar
	ldy #JScr_VBar+2
	lda [Self],y
	sta VBar+2
	dey
	dey
	lda [Self],y
	sta VBar
	ldy #JBar_Value
	lda [VBar],y
	and #$ffff-7
	ldy #JW_YScrld
	sta [Self],y
	
	ldy #JBar_Value
	lda [HBar],y
	and #$ffff-7
	ldy #JW_XScrld
	sta [Self],y
	
	ldy #JW_BackCh
	lda [Self],y
	sta WinP
	iny
	iny
	lda [Self],y
	sta WinP+2
nxsib	lda WinP
	ora WinP+2
	beq donescr
	ldy #JW_Flags
	lda [WinP],y
	and #JF_Meta
	bne nomove
	pea 0
	ldy #JW_YSize
	lda [WinP],y
	pha
	ldy #JW_XSize
	lda [WinP],y
	pha
	ldy #JW_Y
	lda [WinP],y
	pha
	ldy #JW_X
	lda [WinP],y
	pha
	pei (WinP+2)
	pei (WinP)
	jsr @_JWinEGeom
	!POP 14
nomove	ldy #JW_Next
	lda [WinP],y
	tax
	iny
	iny
	lda [WinP],y
	sta WinP+2
	stx WinP
	bra nxsib
donescr	!POST LZ
	rtl

	.)
*/
		
	!PRPCLASS JScr
	
JScrMethods .(
	jmp (Methods,x)
Methods	.long _JCntInit	; Init
	.long _JCntKill	; Destory
	.long _JCntDraw	; Draw
	.long _JCntShow	; Show
	.long _JCntHide	; Hide
	.long _JCntHandle	; Handle
	.long _JCntNotify	; Notify
	.long _JWGetHints ; Notify

	.long _JCntKeyD	; A key down
	.long _JWButton	; Button 
	.long _JWRButton ; Right button (menu)
	.long _JWMotion	; Motion
	.long _JWBound	; Boundary
	.long _JWNotice	; Notice

	.long _JCntAdd	; Add
	.long _JCntRemove ; Remove
	.long _JScrLayout
	
	.long _JScrSync	; Max
	.)

	.)
	
