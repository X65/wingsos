	.(

	.al
	.xl
	.pic
	
	.include	<kern.i65>
	.include	<win.i65>	
	.include	<stdio.i65>
	.include	<widget.i65>
	.include	<dirent.i65>
	.include	<65816.i65>
	.include	<lcc.i65>

/*-----------------------------------------------
     Tree widget - extends scroll region
------------------------------------------------*/

&_JTreeInit	.(

	.abs 1
Root	.long 4
LZ	= *-1
	.dsb 5
Self	.long 4
Model 	.long 4

	.text
	!PRO LZ
	!PRPOBJ JTree
	pea JF_Selectable
	pea WEV_Button
	pea 80
	pea 80
	pei (Self+2)
	pei (Self)
	jsr @_JWInit
	!POP 12	
	lda #32767
	ldy #JW_MaxXS
	sta [Self],y
	ldy #JW_MaxYS
	sta [Self],y
	
	lda Model
	ldy #JTree_Model
	sta [Self],y
	lda Model+2
	iny
	iny
	sta [Self],y
	
	pei (Model+2)
	pei (Model)
	jsr @_TModelRoot
	!POP 4
	phy
	phx
	pea 0
	pea 0
	pei (Self+2)
	pei (Self)
	jsr @_JTreeAddView
	!POP 12
	stx Root
	sty Root+2
	tya
	ldy #JTree_Root+2
	sta [Self],y
	txa
	dey
	dey
	sta [Self],y
	
	ldy #VNode_Flags
	lda [Root],y
	ora #JItemF_Expanded
	sta [Root],y
	
	pei (Root+2)
	pei (Root)
	pei (Self+2)
	pei (Self)
	jsr _JTreeHeight
	!POP 8
	tya
	ldy #JView_MaxY+2
	sta [Self],y
	txa
	dey
	dey
	sta [Self],y
	
	ldy #JW_Colours
	lda [Self],y
	and #$f0
	ora #$01
	sta [Self],y
exeet	ldx Self
	ldy Self+2
	!POST LZ
	rtl
	.)

&_JTreeDraw	.(

	jmp @_GfxClear
	
	.)

_JTreeHeight
	.(
	.abs 1
Vec	.long 4
YSize	.long 4
veclen	.word 2
ptrs	.long 4
item	.long 4
LZ	= *-1
	.dsb 4
Self	.long 4
PareP	.long 4

	.text
	!PRO LZ
	stz YSize
	stz YSize+2
	ldy #VNode_Children
	lda [PareP],y
	sta Vec
	iny
	iny
	lda [PareP],y
	sta Vec+2
	
	lda Vec
	ora Vec+2
	!jeq exeet
	ldy #Vec_size
	lda [Vec],y
	!jeq exeet
	sta veclen
	ldy #Vec_Ptrs
	lda [Vec],y
	sta ptrs
	iny
	iny
	lda [Vec],y
	sta ptrs+2
	
nxone	lda #8
	clc
	adc YSize
	sta YSize
	!ics YSize+2
	
	lda [ptrs]
	sta item
	ldy #2
	lda [ptrs],y
	sta item+2
	
	ldy #VNode_Flags
	lda [item],y
	and #JItemF_Expanded
	beq gonx
	
	pei (item+2)
	pei (item)
	pei (Self+2)
	pei (Self)
	jsr _JTreeHeight
	!POP 8
	txa
	clc
	adc YSize
	sta YSize
	tya
	adc YSize+2
	sta YSize+2
	
gonx	lda ptrs
	clc
	adc #4
	sta ptrs
	!ics ptrs+2
	dec veclen
	!jne nxone
exeet	ldx YSize
	ldy YSize+2
	!POST LZ
	rts
	
	.)


&_JTreeItemClicked
	.(
	.abs 1
LZ	= *-1
	.dsb 5
Self	.long 4
ItemP	.long 4

	.text
	!PRO LZ
	ldy #VNode_Value+2
	lda [ItemP],y
	pha
	dey
	dey
	lda [ItemP],y
	pha
	pei (Self+2)
	pei (Self)
	ldy #JTree_Clicked
	lda [Self],y
	tax
	iny
	iny
	lda [Self],y
	jsr @cb
	!POP 8
	
	ldy #JTree_SelPolicy
	lda [Self],y
	cmp #JTreeP_None
	beq nosel
	
	ldy #VNode_Flags
	lda [ItemP],y
	eor #JItemF_Selected
	sta [ItemP],y
	
	pei (Self+2)
	pei (Self)
	jsr @_JTreeReDrawCols
	!POP 4
nosel	!POST LZ
	rtl
		
	.)


&_JTreeTogExpand
	.(
	.abs 1
YSize	.long 4
LZ	= *-1
	.dsb 5
Self	.long 4
ItemP	.long 4

	.text
	!PRO LZ
	ldy #VNode_Flags
	lda [ItemP],y
	and #JItemF_Expandable
	!jeq nothing
	lda [ItemP],y
	and #JItemF_Expanded
	!jne togexp
	
	ldy #VNode_Children
	lda [ItemP],y
	iny
	iny
	ora [ItemP],y
	!jne togexp
	
	ldy #VNode_Value+2
	lda [ItemP],y
	pha
	dey
	dey
	lda [ItemP],y
	pha
	ldy #JTree_Model+2
	lda [Self],y
	pha
	dey
	dey
	lda [Self],y
	pha
	ldx #MTModel_Expand
	jsr @VMC
	!POP 8

	pei (ItemP+2)
	pei (ItemP)
	pei (Self+2)
	pei (Self)
	jsr @_JTreeSort
	!POP 8 
	
togexp	pei (ItemP+2)
	pei (ItemP)
	pei (Self+2)
	pei (Self)
	jsr _JTreeHeight
	!POP 8
	stx YSize
	sty YSize+2
	
exeet	ldy #VNode_Flags
	lda [ItemP],y
	eor #JItemF_Expanded
	sta [ItemP],y
	clc
	and #JItemF_Expanded
	!jne fixmax
	lda YSize
	eor #$ffff
	sta YSize
	lda YSize+2
	eor #$ffff
	sta YSize+2
	sec
fixmax	ldy #JView_MaxY
	lda [Self],y
	adc YSize
	sta [Self],y
	iny
	iny
	lda [Self],y
	adc YSize+2
	sta [Self],y
	pei (Self+2)
	pei (Self)
	jsr @_JViewSync
	!POP 4
	
redcol	pei (Self+2)
	pei (Self)
	jsr @_JTreeReDrawCols
	!POP 4
nothing	!POST LZ
	rtl
		
	.)

	
&_JTreeReDrawCols
	.(
	.abs 1
WinP	.long 4
LZ	= *-1
	.dsb 5
Self	.long 4

	.text
	!PRO LZ
	ldy #JCnt_BackCh
	lda [Self],y
	sta WinP
	iny
	iny
	lda [Self],y
	sta WinP+2
gochild	lda WinP
	ora WinP+2
	beq nomore
	pei (WinP+2)
	pei (WinP)
	jsr @_JWReDraw
	pla
	pla
nometa	ldy #JW_Next
	lda [WinP],y
	tax
	iny
	iny
	lda [WinP],y
	sta WinP+2
	stx WinP
	bra gochild
nomore
	!POST LZ
	rtl
	.)
	
&_JTreeAddColumns
	.(
	.abs 1
Offs	.word 2
CurP	.long 4
LZ	= *-1
	.dsb 5
Self	.long 4
ColP	.long 4
Title	.long 4
Offset	.long 4
Width	.word 2
Type	.word 2

	.text
	!PRO LZ
	stz Offs
	
nxcol	ldx Offs
	lda Title,x
	ora Title+2,x
	!jeq exeet
	lda Type,x
	pha
	lda Offset+2,x
	pha
	lda Offset,x
	pha
	lda Width,x
	pha
	lda Title+2,x
	pha
	lda Title,x
	pha
	pei (Self+2)
	pei (Self)
	pea 0
	pea 0
	jsr @_JTreeColInit
	!POP 20
	stx CurP
	sty CurP+2
	
	ldy #JTree_SortCol
	lda [Self],y
	iny
	iny
	ora [Self],y
	!jne alrs
	lda CurP
	ldy #JTree_SortCol
	sta [Self],y
	iny
	iny
	lda CurP+2
	sta [Self],y
alrs	pei (CurP+2)
	pei (CurP)
	pei (Self+2)
	pei (Self)
	ldx #MJCnt_Add
	jsr @VMC
	!POP 8
	
	lda ColP
	ora ColP+2
	beq nocolp
	lda CurP
	sta [ColP]
	lda CurP+2
	ldy #2
	sta [ColP],y
	lda #4
	clc
	adc ColP
	sta ColP
	!ics ColP+2
nocolp	lda #12
	clc
	adc Offs
	sta Offs
	!jra nxcol
	
exeet	!POST LZ
	rtl
	.)

&_JTreeScrolled 
	.(

	.abs 1
LZ	= *-1
	.dsb 5
Self	.long 4
ScrX	.long 4
ScrY	.long 4

	.text
	
	!PRO LZ
	lda ScrY
	and #$ffff-7
	ldy #JTree_YScroll
	cmp [Self],y
	!jeq exeet
	sta [Self],y
	pei (Self+2)
	pei (Self)
	jsr @_JTreeReDrawCols
	pla
	pla
exeet	!POST LZ
	rtl

	.)
	
_JTreeIsVis
	.(
	.abs 1
LZ	= *-1
	.dsb 4
View	.long 4
	.text
	!PRO LZ
nxv	ldx #0
	ldy #VNode_Flags
	lda [View],y
	and #JItemF_Expanded
	!jeq exeet
	ldy #VNode_Parent
	lda [View],y
	tax
	iny
	iny
	lda [View],y
	sta View+2
	stx View
	ora View
	!jne nxv
	ldx #1
exeet	!POST LZ
	rts	
	.)
	
	!PRPCLASS JTree
		
JTreeMethods	.(
	jmp (Methods,x)
Methods	.long _JCntInit	; Init
	.long _JCntKill	; Destory
	.long _JTreeDraw	; Draw
	.long _JCntShow	; Show
	.long _JCntHide	; Hide
	.long _JCntHandle	; Handle
	.long _JViewNotify	; Notify
	.long _JCntGetHints ; Notify

	.long _JWKeyD	; A key down
	.long _JWButton	; Button 
	.long _JWRButton ; Right button (menu)
	.long _JWMotion	; Motion
	.long _JWBound	; Boundary
	.long _JWNotice	; Notice

	.long _JCntAdd	; Add
	.long _JCntRemove ; Remove
	.long _JCntLayout
	
	.long _JTreeScrolled ; scrolled
	
	.)

	.)
	
