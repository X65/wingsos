	.(

	.al
	.xl
	.pic
	
	.include	<kern.i65>
	.include	<win.i65>	
	.include	<stdio.i65>
	.include	<widget.i65>
	.include	<lcc.i65>


/*-----------------------------------------------
           Text field Widget - Jwin
------------------------------------------------*/

&_JStxInit	.(
	.abs 1
LZ	= *-1
	.dsb 5
Self	.long 4
Label	.long 4

	.text
	!PRO LZ
	!PRPOBJ JStx
	pea JF_Selectable
	pea WEV_Button+WEV_Boundary
	pea 16
	pea 80
	pei (Self+2)
	pei (Self)
	jsr @_JWInit
	!POP 12

/*	pea 0
	pea WEV_Draw+WEV_Expose
	pei (Self+2)
	pei (Self)
	jsr @_JWinOpaque
	pla
	pla
	pla
	pla */
	
	
	pei (Label+2)
	pei (Label)
	pei (Self+2)
	pei (Self)
	jsr @_JStxSetText
	!POP 8
	
	
exeet	ldx Self
	ldy Self+2
	!POST LZ
	rtl
	.)

&_JStxSetText
	.(
	.abs 1
LZ	= *-1
	.dsb 5
Self	.long 4
Label	.long 4

	.text
	!PRO LZ
	pei (Label+2)
	pei (Label)
	jsr @_strdup
	pla
	pla
	tya
	ldy #JStx_Label+2
	sta [Self],y
	txa
	dey
	dey
	sta [Self],y
	pei (Self+2)
	pei (Self)
	jsr @_JStxValidate
	!POP 4
	!POST LZ
	rtl
	
	.)

&_JStxValidate
	.(
	.abs 1
PrefX	.word 2
PrefY	.word 2
LZ	= *-1
	.dsb 5
Self	.long 4

	.text
	!PRO LZ
	pea 0
	ldy #JW_Font
	lda [Self],y
	pha
	ldy #JStx_Label+2
	lda [Self],y
	pha
	dey
	dey
	lda [Self],y
	pha
	jsr @_FL_extStrX
	!POP 8
	txa
	ldy #JStx_LenX
	sta [Self],y
	sta PrefX
	eor #7
	inc
	and #7
	clc
	adc PrefX
	sta PrefX
	
	ldy #JW_Font
	lda [Self],y
	pha
	jsr @_FL_extY
	pla
	stx PrefY
	txa
	eor #7
	inc
	and #7
	clc
	adc PrefY
	sta PrefY
	pei (PrefY)
	pei (PrefX)
	pei (Self+2)
	pei (Self)
	jsr @_JWSetAll
	!POP 8
	!POST LZ
	rtl
	
	.)
	
&_JStxDraw	.(

	.abs 1
Y8	.word 2
Yab	.word 2
LZ	= *-1
	.dsb 5
Self	.long 4
	
	.text
	!PRO LZ
	jsr @_GfxClear
	pea GMOD_SameBack+GMOD_SamePen+GMOD_Ora
	jsr @_GfxSetMode
	pla

	ldy #JW_Font
	lda [Self],y
	pha
	jsr @_FL_extY
	pla
	stx Y8
	
	ldy #JW_Font
	lda [Self],y
	pha
	jsr @_FL_extY
	pla
	stx Yab
	
	; NOTE assumes Y size of 8

	ldy #JStx_Mode
	lda [Self],y
	and #STXM_CenterY
	beq nocenty
	ldy #JW_YSize
	lda [Self],y
	sec
	sbc Y8
	lsr
nocenty	clc
	adc Yab
	pha
	
	ldy #JStx_Mode
	lda [Self],y
	and #STXM_CenterX
	beq nocentx
	
	ldy #JW_XSize
	lda [Self],y
	sec
	ldy #JStx_LenX
	sbc [Self],y
	bcs isok
	lda #0
isok	lsr
nocentx	pha
	jsr @_GfxSetPen
	pla
	pla
	
	ldy #JStx_Label+2
	lda [Self],y
	pha
	dey
	dey
	lda [Self],y
	pha
	jsr @_GfxText
	pla
	pla
	
exeet	!POST LZ
	rtl
	
	.)
	
	!PRPCLASS JStx

JStxMethods .(
	jmp (Methods,x)
Methods	.long _JWInit	; Init
	.long _JWKill	; Destory
	.long _JStxDraw	; Draw
	.long _JWShow	; Show
	.long _JWHide	; Hide
	.long _JWHandle	; Handle
	.long _JWNotify	; Notify
	.long _JWGetHints ; Notify

	.long _JWKeyD	; A key down
	.long _JWButton ; Button 
	.long _JWRButton ; Right button (menu)
	.long _JWMotion	; Motion
	.long _JWBound ; Boundary
	.long _JWNotice	; Notice
	.)

	.)
	
