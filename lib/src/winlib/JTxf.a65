	.(

	.al
	.xl
	.pic
	
	.include	<kern.i65>
	.include	<win.i65>	
	.include	<stdio.i65>
	.include	<widget.i65>
	.include	<termio.i65>
	.include	<lcc.i65>


/*-----------------------------------------------
           Text field Widget - Jwin
------------------------------------------------*/

&_JTxfInit	.(

	.abs 1
LZ	= *-1
	.dsb 5

Self	.long 4

	.text
	!PRO LZ
	!PRPOBJ JTxf
	pea JF_Selectable
	pea WEV_Button+WEV_Boundary
	pea 8
	pea 80
	pei (Self+2)
	pei (Self)
	jsr @_JWInit
	!POP 12
	lda #4
	ldy #JTxf_OffsX
	sta [Self],y
	pea 8
	pea 32767
	pei (Self+2)
	pei (Self)
	jsr @_JWSetMax
	!POP 8
	pea ^blank
	pea !blank
	pei (Self+2)
	pei (Self)
	jsr @_JTxfSetText
	!POP 8
exeet	ldx Self
	ldy Self+2
	!POST LZ
	rtl
blank	.byte 0
	.)

_JTxfCalc	.(

	.abs 1
XUpto	.word 2
ChrUp	.word 2
Upto	.long 4
LZ	= *-1
	.dsb 4

Self	.long 4

	.text

	!PRO LZ
	stz XUpto
	stz ChrUp
	ldy #JW_FStyle
	lda [Self],y
	pha
	ldy #JW_Font
	lda [Self],y
	pha
	jsr @_FL_prepSize
	pla
	pla
	ldy #JTxf_String
	lda [Self],y
	sta Upto
	iny
	iny
	lda [Self],y
	sta Upto+2
	
more	lda ChrUp
	ldy #JTxf_XCur
	cmp [Self],y
	bcs fine
	lda [Upto]
	and #$ff
	!INP Upto
	tay
	jsr @FL_getXSize
	clc
	adc XUpto
	sta XUpto
	inc ChrUp
	bra more
	
fine	lda XUpto
	ldy #JTxf_CursX
	sta [Self],y
	clc
	ldy #JTxf_OffsX
	adc [Self],y
	sta ChrUp
	bmi islow
	cmp #4
	bcs isvis
islow	lda #4
	bra poscurs
isvis	ldy #JW_XSize
	lda [Self],y
	sec
	sbc ChrUp
	bcc scrit
	cmp #4
	bcs exeet
scrit	ldy #JW_XSize
	lda [Self],y
	sec
	sbc #4
poscurs	sec
	sbc XUpto
	ldy #JTxf_OffsX
	sta [Self],y
exeet	!POST LZ
	rts

	.)
	

&_JTxfDraw	.(

	.abs 1
X8	.word 2
Y8	.word 2
LZ	= *-1
	.dsb 5

Self	.long 4

	.text
	!PRO LZ
	pea $01
	jsr @_GfxSetCol
	pla
	
	jsr @_GfxClear
/*	ldy #JW_XSize
	lda [Self],y
	lsr
	lsr
	lsr
	sta X8
	dec
	dec
	pha
	pha
	pea $01
	ldy #JW_YSize
	lda [Self],y
	lsr
	lsr
	lsr
	sta Y8
	pha
	pei (X8)
	pea ^TxfChar
	pea !TxfChar
	pea ^TxfStr
	pea !TxfStr
	jsr @_GfxString
	tsc
	clc
	adc #18
	tcs*/

	pea GMOD_SameBack+GMOD_SamePen+GMOD_Ora
	jsr @_GfxSetMode
	pla
	
	pea 7
	ldy #JTxf_OffsX
	lda [Self],y
	pha
	jsr @_GfxSetPen
	pla
	pla
	
	ldy #JTxf_String+2
	lda [Self],y
	pha
	dey
	dey
	lda [Self],y
	pha
	jsr @_GfxText
	pla
	pla
	
	ldy #JW_Flags
	lda [Self],y
	and #JF_Focused
	beq exeet
	pea 1
	ldy #JTxf_CursX
	lda [Self],y
	clc
	ldy #JTxf_OffsX
	adc [Self],y
	pha
	jsr @_GfxSetPen
	pla
	pla

	pea 6
	pea 1
	pea ^ABox
	pea !ABox
	jsr @_GfxString
	pla
	pla
	pla
	pla
	
exeet	!POST LZ
	rtl
/*
TxfStr	.byte GFX_Charset,"%D%b%b"
	.byte CHAR_Col,"%b"
	.byte 0,CHAR_Rep,"%b",1,2
	.byte 3,CHAR_Rep,"%b",4,5
	.byte CHAR_End
	.byte "%E"
*/	
ABox	.byte GFX_Box
	.byte "%d%d"
	.byte 1
	.byte "%E"

	.)

&_JTxfKeyD	.(
	
	.abs 1
StrP	.long 4
Pos	.word 2
LSz	.word 2
Up	.word 2
Up2	.word 2
LZ	= *-1
	.dsb 5

Self	.long 4
Key	.word 2

	.text
	!PRO LZ
	ldy #JTxf_String
	lda [Self],y
	sta StrP
	iny
	iny
	lda [Self],y
	sta StrP+2
	
	ldy #JTxf_XCur
	lda [Self],y
	sta Pos
	
	ldy #JTxf_Size
	lda [Self],y
	sta LSz
	inc
	pha
	pei (Self+2)
	pei (Self)
	jsr _JTxfMakeMin
	pla
	pla
	pla
	
	lda Key
	cmp #BS
	bne notbs
isbs	lda Pos
	beq anoup
	dec Pos
	cmp LSz
	bne noteol
	dec LSz
	ldy LSz
	!AS
	lda #0
	sta [StrP],y
	!AL
	bra doup
noteol	lda Pos
	sta Up2
	inc
	sta Up
	jsr delStr	
	dec LSz
	bra doup
anoup	brl noupdate
	
notbs	cmp #CR
	bne notret
	
	pei (Self+2)
	pei (Self)
	ldy #JTxf_Entered
	lda [Self],y
	tax
	iny
	iny
	lda [Self],y
	jsr @cb
	pla
	pla
gonoup	brl noupdate
	
notret	cmp #CURL
	bne notcl
	lda Pos
	beq gonoup
	dec Pos
	bra doup
	
notcl	cmp #CURR
	bne notcr
	lda Pos
	cmp LSz
	beq gonoup
	inc Pos
	bra doup
	
notcr	ldx Pos
	cpx LSz
	beq atend
	pha
	lda LSz
	sta Up
	inc
	sta Up2
	jsr insStr
	pla
	!AS
atend	ldy Pos
	sta [StrP],y
	!AL
	inc Pos
	inc LSz

doup	lda Pos
	ldy #JTxf_XCur
	sta [Self],y
	
	lda LSz
	ldy #JTxf_Size
	sta [Self],y
	
	pei (Self+2)
	pei (Self)
	jsr _JTxfCalc
	jsr @_JWReDraw
	pla
	pla
	
noupdate	
	!POST LZ
	rtl
	
insStr	!AS
	ldy Up
	lda [StrP],y
	ldy Up2
	sta [StrP],y
	!AL
	dec Up
	dec Up2
	lda Up2
	cmp Pos
	beq nome
	bcs insStr
nome	rts

delStr	!AS
	ldy Up
	lda [StrP],y
	ldy Up2
	sta [StrP],y
	!AL
	inc Up
	inc Up2
	lda Up2
	cmp LSz
	bcc delStr
	rts

	.)

&_JTxfGetText .(
	.abs 1
LZ	= *-1
	.dsb 5
Self	.long 4

	.text
	!PRO LZ
	ldy #JTxf_String
	lda [Self],y
	tax
	iny
	iny
	lda [Self],y
	tay
	!POST LZ
	rtl
	
	.)

_JTxfMakeMin .(
	.abs 1
LZ	= *-1
	.dsb 4

Self	.long 4
Size	.word 2
	.text
	!PRO LZ
	inc Size
	ldy #JTxf_AcSize
	lda [Self],y
	cmp Size
	bcs exeet
	lda Size
	clc
	adc #128
	ldy #JTxf_AcSize
	sta [Self],y
	pea 0
	pha
	ldy #JTxf_String+2
	lda [Self],y
	pha
	dey
	dey
	lda [Self],y
	pha
	jsr @_realloc
	pla
	pla
	pla
	pla
	tya
	ldy #JTxf_String+2
	sta [Self],y
	txa
	dey
	dey
	sta [Self],y
exeet	!POST LZ
	rts
	
	.)
	
&_JTxfButton .(
	.abs 1
XUpto	.word 2
ChrUp	.word 2
Upto	.long 4
LZ	= *-1
	.dsb 5
Self	.long 4
Subtype	.word 2
X	.word 2
Y	.word 2
XAbs	.word 2
YAbs	.word 2
	.text
	
	!PRO LZ
	lda Subtype
	cmp #EVS_But1Down
	beq fincur
	brl exeet
fincur	ldy #JW_FStyle
	lda [Self],y
	pha
	ldy #JW_Font
	lda [Self],y
	pha
	jsr @_FL_prepSize
	pla
	pla
	stz ChrUp
	lda X
	sec
	ldy #JTxf_OffsX
	sbc [Self],y
	sta XUpto
	ldy #JTxf_String
	lda [Self],y
	sta Upto
	iny
	iny
	lda [Self],y
	sta Upto+2
	dec ChrUp
more	inc ChrUp
	lda [Upto]
	and #$ff
	beq eol
	!INP Upto
	tay
	jsr @FL_getXSize
	pha
	lda XUpto
	sec
	sbc 1,s
	sta XUpto
	plx
	tax
	bpl more
eol	lda ChrUp
	ldy #JTxf_XCur
	sta [Self],y
	pei (Self+2)
	pei (Self)
	jsr _JTxfCalc
	jsr @_JWReDraw
	pla
	pla
	
exeet	!POST LZ
	rtl

	.)

&_JTxfSetText .(
	.abs 1
LZ	= *-1
	.dsb 5
Self	.long 4
StrP	.long 4
	.text

	!PRO LZ
	ldy #JTxf_XCur
	lda #0
	sta [Self],y
	pei (StrP+2)
	pei (StrP)
	jsr @_strlen
	pla
	pla
	phx
	ldy #JTxf_Size
	txa
	sta [Self],y
	pei (Self+2)
	pei (Self)
	jsr _JTxfMakeMin
	pla
	pla
	pla
	pei (StrP+2)
	pei (StrP)
	ldy #JTxf_String+2
	lda [Self],y
	pha
	dey
	dey
	lda [Self],y
	pha
	jsr @_strcpy
	pei (Self+2)
	pei (Self)
	jsr _JTxfCalc
	jsr @_JWReDraw
	!POST LZ+12
	rtl
	
	.)
	
	!PRPCLASS JTxf
	
JTxfMethods .(
	jmp (Methods,x)
Methods	.long _JWInit	; Init
	.long _JWKill	; Destory
	.long _JTxfDraw	; Draw
	.long _JWShow	; Show
	.long _JWHide	; Hide
	.long _JWHandle	; Handle
	.long _JWNotify	; Notify
	.long _JWGetHints ; Notify

	.long _JTxfKeyD	; A key down
	.long _JTxfButton ; Button 
	.long _JWRButton ; Right button (menu)
	.long _JWMotion	; Motion
	.long _JWBound ; Boundary
	.long _JWNotice	; Notice
	
	.)

TxfChar	.byte $ff,$80,$80,$80,$80,$80,$80,$80
	.byte $ff,$00,$00,$00,$00,$00,$00,$00
	.byte $ff,$01,$01,$01,$01,$01,$01,$01
	.byte $80,$80,$80,$80,$80,$80,$80,$ff
	.byte $00,$00,$00,$00,$00,$00,$00,$ff
	.byte $01,$01,$01,$01,$01,$01,$01,$ff

	.)
	
